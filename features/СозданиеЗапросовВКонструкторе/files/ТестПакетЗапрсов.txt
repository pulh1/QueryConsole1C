ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	Сотрудники.Ссылка КАК Сотрудник,
	&ДатаНачала КАК ДатаНачала,
	&ДатаОкончания КАК ДатаОкончания
ПОМЕСТИТЬ ВТФильтрКадровойИстории
ИЗ
	Справочник.Сотрудники КАК Сотрудники
;
////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ РАЗРЕШЕННЫЕ
	КадроваяИсторияСотрудниковИнтервалы.Сотрудник КАК Сотрудник,
	КадроваяИсторияСотрудниковИнтервалы.Организация КАК Организация,
	КадроваяИсторияСотрудниковИнтервалы.Подразделение КАК Подразделение,
	КадроваяИсторияСотрудниковИнтервалы.Должность КАК Должность,
	КадроваяИсторияСотрудниковИнтервалы.ДолжностьПоШтатномуРасписанию КАК ДолжностьПоШтатномуРасписанию,
	КадроваяИсторияСотрудниковИнтервалы.НачалоПериода КАК НачалоПериода,
	КадроваяИсторияСотрудниковИнтервалы.КонецПериода КАК КонецПериода,
	КадроваяИсторияСотрудниковИнтервалы.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	КадроваяИсторияСотрудниковИнтервалы.КоличествоСтавок КАК КоличествоСтавок,
	КадроваяИсторияСотрудниковИнтервалы.ВидСобытия КАК ВидСобытия
ПОМЕСТИТЬ ВТИнтервалыКадровойИстории
ИЗ
	ИсполняемоеПредставление.РегистрСведений.КадроваяИсторияСотрудников.Периоды(
		(Сотрудник, ДатаНачала, ДатаОкончания) В 
		(ВЫБРАТЬ
			ВТФильтрКадровойИстории.Сотрудник КАК Сотрудник,
			ВТФильтрКадровойИстории.ДатаНачала КАК ДатаНачала,
			ВТФильтрКадровойИстории.ДатаОкончания КАК ДатаОкончания
		ИЗ
			ВТФильтрКадровойИстории КАК ВТФильтрКадровойИстории),
		ВключатьЗаписиНаНачалоПериода = ИСТИНА) КАК КадроваяИсторияСотрудниковИнтервалы
ГДЕ
	КадроваяИсторияСотрудниковИнтервалы.Организация = &Организация
		И КадроваяИсторияСотрудниковИнтервалы.Подразделение В ИЕРАРХИИ (&Подразделение)
	И КадроваяИсторияСотрудниковИнтервалы.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
;
////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ РАЗЛИЧНЫЕ
	ИнтервалыКадровойИстории.Сотрудник КАК Сотрудник,
	&ДатаНачала КАК ДатаНачала,
	&ДатаОкончания КАК ДатаОкончания
ПОМЕСТИТЬ ВТСотрудники
ИЗ
	ВТИнтервалыКадровойИстории КАК ИнтервалыКадровойИстории
ГДЕ
	ИнтервалыКадровойИстории.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
;
////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ДанныеПроизводственногоКалендаряПомесячно.Год КАК Год,
	СУММА(ДанныеПроизводственногоКалендаряПомесячно.РабочихДней) / 12 КАК РабочихДней,
	СУММА(ДанныеПроизводственногоКалендаряПомесячно.РабочихЧасовДлительностьНедели40Часов) / 12 КАК РабочихЧасов
ПОМЕСТИТЬ ВТДанныеКалендаря
ИЗ
	РегистрСведений.ДанныеПроизводственногоКалендаряПомесячно КАК ДанныеПроизводственногоКалендаряПомесячно
ГДЕ
	ДанныеПроизводственногоКалендаряПомесячно.ПроизводственныйКалендарь = &КалендарьРФ
	И ДанныеПроизводственногоКалендаряПомесячно.Год МЕЖДУ ГОД(&ДатаНачала) И ГОД(&ДатаОкончания)

СГРУППИРОВАТЬ ПО
	ДанныеПроизводственногоКалендаряПомесячно.Год
;
////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	КадроваяИсторияСотрудниковЗаписи.Сотрудник КАК Сотрудник,
	ВЫБОР
		КОГДА КадроваяИсторияСотрудниковЗаписи.ПериодЗаписи < &ДатаНачала
			ТОГДА &ДатаНачала
		ИНАЧЕ КадроваяИсторияСотрудниковЗаписи.ПериодЗаписи
	КОНЕЦ КАК ПериодЗаписи,
	КадроваяИсторияСотрудниковЗаписи.ДолжностьПоШтатномуРасписанию КАК ДолжностьПоШтатномуРасписанию
ПОМЕСТИТЬ ВТПериодыИзмененияДанных
ИЗ
	ИсполняемоеПредставление.РегистрСведений.КадроваяИсторияСотрудников.Записи(
		(Сотрудник, ДатаНачала, ДатаОкончания) В 
		(ВЫБРАТЬ
			ВТСотрудники.Сотрудник КАК Сотрудник,
			ВТСотрудники.ДатаНачала КАК ДатаНачала,
			ВТСотрудники.ДатаОкончания КАК ДатаОкончания
		ИЗ
			ВТСотрудники КАК ВТСотрудники),
		ВключатьЗаписиНаНачалоПериода = ИСТИНА) КАК КадроваяИсторияСотрудниковЗаписи

ОБЪЕДИНИТЬ

ВЫБРАТЬ РАЗЛИЧНЫЕ
	ПлановыйФОТИтогиЗаписи.Сотрудник,
	ПлановыйФОТИтогиЗаписи.ПериодЗаписи,
	ВТИнтервалыКадровойИстории.ДолжностьПоШтатномуРасписанию
ИЗ
	ИсполняемоеПредставление.РегистрСведений.ПлановыйФОТИтоги.Записи(
		(Сотрудник, ДатаНачала, ДатаОкончания) В 
		(ВЫБРАТЬ
			ВТСотрудники.Сотрудник КАК Сотрудник,
			ВТСотрудники.ДатаНачала КАК ДатаНачала,
			ВТСотрудники.ДатаОкончания КАК ДатаОкончания
		ИЗ
			ВТСотрудники КАК ВТСотрудники)
) КАК ПлановыйФОТИтогиЗаписи
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИнтервалыКадровойИстории КАК ВТИнтервалыКадровойИстории
		ПО ПлановыйФОТИтогиЗаписи.Сотрудник = ВТИнтервалыКадровойИстории.Сотрудник
		И ПлановыйФОТИтогиЗаписи.ПериодЗаписи МЕЖДУ ВТИнтервалыКадровойИстории.НачалоПериода И ВТИнтервалыКадровойИстории.КонецПериода
ГДЕ
	ВТИнтервалыКадровойИстории.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)

ОБЪЕДИНИТЬ

ВЫБРАТЬ РАЗЛИЧНЫЕ
	ВТСотрудники.Сотрудник,
	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВТСотрудники.ДатаНачала, ГОД), ГОД, ВТДанныеКалендаря.Год - ГОД(ВТСотрудники.ДатаНачала)),
	ВТИнтервалыКадровойИстории.ДолжностьПоШтатномуРасписанию
ИЗ
	ВТСотрудники КАК ВТСотрудники
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеКалендаря КАК ВТДанныеКалендаря
		ПО ГОД(ВТСотрудники.ДатаНачала) < ВТДанныеКалендаря.Год
		И ГОД(ВТСотрудники.ДатаОкончания) >= ВТДанныеКалендаря.Год
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИнтервалыКадровойИстории КАК ВТИнтервалыКадровойИстории
		ПО ВТСотрудники.Сотрудник = ВТИнтервалыКадровойИстории.Сотрудник
		И ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВТСотрудники.ДатаНачала, ГОД), ГОД, ВТДанныеКалендаря.Год - ГОД(ВТСотрудники.ДатаНачала)) МЕЖДУ ВТИнтервалыКадровойИстории.НачалоПериода И ВТИнтервалыКадровойИстории.КонецПериода
ГДЕ
	ВТИнтервалыКадровойИстории.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
;
////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ РАЗРЕШЕННЫЕ
	ВТПериодыИзмененияДанных.Сотрудник КАК Сотрудник,
	ДанныеСотрудников.ТабельныйНомер КАК ТабельныйНомер,
	ДанныеСотрудников.ДатаПриема КАК ДатаПриема,
	ВТПериодыИзмененияДанных.ПериодЗаписи КАК ПериодЗаписи,
	ВТИнтервалыКадровойИстории.ДолжностьПоШтатномуРасписанию КАК ПозицияПШР,
	ВТИнтервалыКадровойИстории.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	ВТИнтервалыКадровойИстории.Подразделение КАК Подразделение,
	ПлановыйФОТИтогиСрезПоследних.ТарифнаяСтавка КАК ОкладТариф,
	ВЫБОР
		КОГДА ПлановыйФОТИтогиСрезПоследних.ВидТарифнойСтавки = ЗНАЧЕНИЕ(Перечисление.ВидыТарифныхСтавок.ЧасоваяТарифнаяСтавка)
			ТОГДА ПлановыйФОТИтогиСрезПоследних.ТарифнаяСтавка * ВТДанныеКалендаря.РабочихЧасов
		КОГДА ПлановыйФОТИтогиСрезПоследних.ВидТарифнойСтавки = ЗНАЧЕНИЕ(Перечисление.ВидыТарифныхСтавок.ДневнаяТарифнаяСтавка)
			ТОГДА ПлановыйФОТИтогиСрезПоследних.ТарифнаяСтавка * ВТДанныеКалендаря.РабочихДней
		ИНАЧЕ ПлановыйФОТИтогиСрезПоследних.ТарифнаяСтавка
	КОНЕЦ КАК МесячнаяТарифнаяСтавка,
	ВТИнтервалыКадровойИстории.КоличествоСтавок КАК Процент,
	"RUB" КАК Валюта
ИЗ
	ВТПериодыИзмененияДанных КАК ВТПериодыИзмененияДанных
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИнтервалыКадровойИстории КАК ВТИнтервалыКадровойИстории
		ПО ВТПериодыИзмененияДанных.Сотрудник = ВТИнтервалыКадровойИстории.Сотрудник
		И ВТПериодыИзмененияДанных.ПериодЗаписи МЕЖДУ ВТИнтервалыКадровойИстории.НачалоПериода И ВТИнтервалыКадровойИстории.КонецПериода
		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудники КАК ВТСотрудники
		ПО ВТПериодыИзмененияДанных.Сотрудник = ВТСотрудники.Сотрудник
		ЛЕВОЕ СОЕДИНЕНИЕ ИсполняемоеПредставление.РегистрСведений.ПлановыйФОТИтоги.СрезПоследних(
			(Сотрудник, Период) В 
			(ВЫБРАТЬ
				ВТПериодыИзмененияДанных.Сотрудник КАК Сотрудник,
				ВТПериодыИзмененияДанных.ПериодЗаписи КАК Период
			ИЗ
				ВТПериодыИзмененияДанных КАК ВТПериодыИзмененияДанных)
) КАК ПлановыйФОТИтогиСрезПоследних
		ПО ВТПериодыИзмененияДанных.Сотрудник = ПлановыйФОТИтогиСрезПоследних.Сотрудник
		И ВТПериодыИзмененияДанных.ПериодЗаписи = ПлановыйФОТИтогиСрезПоследних.Период
		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеКалендаря КАК ВТДанныеКалендаря
		ПО ГОД(ВТПериодыИзмененияДанных.ПериодЗаписи) = ВТДанныеКалендаря.Год
		ЛЕВОЕ СОЕДИНЕНИЕ ИсполняемоеПредставление.ДанныеСотрудников(
			(Сотрудник, Период) В 
			(ВЫБРАТЬ
				ВТСотрудники.Сотрудник КАК Сотрудник,
				ВТСотрудники.ДатаОкончания КАК ДатаОкончания
			ИЗ
				ВТСотрудники КАК ВТСотрудники)
) КАК ДанныеСотрудников
		ПО ВТПериодыИзмененияДанных.Сотрудник = ДанныеСотрудников.Сотрудник
УПОРЯДОЧИТЬ ПО
	Сотрудник,
	ПериодЗаписи
