Функция ПолучитьДанные(СписокСотрудников, ПериодДействия, Перерасчет)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
	Запрос.УстановитьПараметр("ПериодДействия", ПериодДействия);
	Запрос.УстановитьПараметр("Перерасчет", Перерасчет);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&ПериодДействия КАК ПериодДействия,
	|	Сотрудники.Ссылка КАК Сотрудник
	|ПОМЕСТИТЬ ВТФильтрНачисленияБаза
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&СписокСотрудников)";
	Запрос.Выполнить();
	ЗапросПолученияДанныхРегистра = Новый Запрос;
	ЗапросПолученияДанныхРегистра.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	ЗапросПолученияДанныхРегистра.Текст = 
	"ВЫБРАТЬ
	|	РегистрРасчета.Регистратор КАК Регистратор,
	|	РегистрРасчета.НомерСтроки КАК НомерСтроки,
	|	РегистрРасчета.Сотрудник КАК Сотрудник,
	|	РегистрРасчета.ВидРасчета КАК ВидРасчета,
	|	РегистрРасчета.ПериодРегистрации КАК ПериодРегистрации,
	|	РегистрРасчета.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	РегистрРасчета.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	РегистрРасчета.Сторно КАК Сторно,
	|	РегистрРасчета.СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	РегистрРасчета.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
	|	РегистрРасчета.РегистраторРазовогоНачисления КАК РегистраторРазовогоНачисления,
	|	РегистрРасчета.ПериодДействия КАК ПериодДействия,
	|	РегистрРасчета.Результат КАК Результат
	|ИЗ
	|	РегистрРасчета.Начисления КАК РегистрРасчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФильтрНачисленияБаза КАК Фильтр
	|		ПО (РегистрРасчета.ПериодДействия = Фильтр.ПериодДействия
	|				И РегистрРасчета.Сотрудник = Фильтр.Сотрудник
	|				И РегистрРасчета.ВидРасчета.ТребуетсяРасчетБазы)
	|ГДЕ
	|	РегистрРасчета.Перерасчет = &Перерасчет";
	ЗапросПолученияДанныхРегистра.УстановитьПараметр("Перерасчет", Перерасчет);

	ДанныеРегистра = ЗапросПолученияДанныхРегистра.Выполнить().Выгрузить();
	ОписаниеРассчитываемогоНабора = Обработки.РасчетБазыНачисленийУдержаний.НовыйОписаниеРассчитываемогоНабораНачисленийТаблица();
	Для Каждого СтрокаРегистра Из ДанныеРегистра Цикл
		СтрокаРассчитываемогоНабора = ОписаниеРассчитываемогоНабора.РассчитываемыеЗаписи.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРассчитываемогоНабора, СтрокаРегистра);
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	РасчетБазыНачислений = Обработки.РасчетБазыНачисленийУдержаний.Создать();
	УстановитьПривилегированныйРежим(Ложь);
	
	МенеджерРасчетаЗП = РасчетЗарплатыРасширенный.СоздатьМенеджерРасчета(
		КонецМесяца(ЗарплатаКадрыПериодическиеРегистры.МаксимальнаяДата()), 
		Справочники.Организации.ПустаяСсылка());

	НастройкиРасчетаБазы = Обработки.РасчетБазыНачисленийУдержаний.НовыйНастройкиРасчетаБазыНачислений();
	НастройкиРасчетаБазы.ПолучатьПодробнуюРасшифровку = Истина;	
	РасчетБазыНачислений.ИнициализироватьРасчетБазыНачислений(
		ОписаниеРассчитываемогоНабора, 
		МенеджерРасчетаЗП, 
		НастройкиРасчетаБазы);	
	РасчетнаяБаза = РасчетБазыНачислений.РасчетнаяБазаНачислений();
	РасчетнаяБаза.Свернуть("ВидРасчетаРазрез,РегистраторРазрез,НомерСтрокиРазрез,Регистратор,НомерСтроки","РезультатБаза");

	Результат = Новый ТаблицаЗначений;
	ИспользуемыеПоля = Новый Соответствие();
	ИспользуемыеПоля.Вставить("РЕГИСТРАТОРРАЗРЕЗ", Истина);
	ИспользуемыеПоля.Вставить("ВИДРАСЧЕТАРАЗРЕЗ", Истина);
	ИспользуемыеПоля.Вставить("НОМЕРСТРОКИРАЗРЕЗ", Истина);
	ИспользуемыеПоля.Вставить("РЕЗУЛЬТАТ", Истина);
	ИспользуемыеПоля.Вставить("ПЕРИОДДЕЙСТВИЯ", Истина);
	ИспользуемыеПоля.Вставить("ВИДРАСЧЕТА", Истина);
	ИспользуемыеПоля.Вставить("РЕЗУЛЬТАТБАЗА", Истина);
	ИспользуемыеПоля.Вставить("СОТРУДНИК", Истина);
	Для Каждого Колонка Из ДанныеРегистра.Колонки Цикл
		Если ИспользуемыеПоля.Получить(ВРег(Колонка.Имя)) <> Неопределено Тогда
			Результат.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
		КонецЕсли;
	КонецЦикла;
	Для Каждого Колонка Из РасчетнаяБаза.Колонки Цикл
		Если ИспользуемыеПоля.Получить(ВРег(Колонка.Имя)) <> Неопределено Тогда
			Результат.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
		КонецЕсли;
	КонецЦикла;
	
	ДанныеРегистра.Индексы.Добавить("Регистратор, НомерСтроки");
	СтруктураПоиска = Новый Структура("Регистратор, НомерСтроки");
	
	Для Каждого СтрокаБазы Из РасчетнаяБаза Цикл
		СтрокаРезультат = Результат.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРезультат, СтрокаБазы);
		СтруктураПоиска.НомерСтроки = СтрокаБазы.НомерСтроки;
		СтруктураПоиска.Регистратор = СтрокаБазы.Регистратор;
		
		СтрокаДанныхРегистра = ДанныеРегистра.НайтиСтроки(СтруктураПоиска)[0];
		ЗаполнитьЗначенияСвойств(СтрокаРезультат, СтрокаДанныхРегистра);
	КонецЦикла;

	ЗапросРезультат = Новый Запрос();
	ЗапросРезультат.Текст = 
	"ВЫБРАТЬ
	|	РегистрРасчетаБаза.ВидРасчета КАК ВидРасчета,
	|	РегистрРасчетаБаза.ПериодДействия КАК ПериодДействия,
	|	РегистрРасчетаБаза.Сотрудник КАК Сотрудник,
	|	РегистрРасчетаБаза.Результат КАК Результат,
	|	РегистрРасчетаБаза.РезультатБаза КАК РезультатБаза,
	|	РегистрРасчетаБаза.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
	|	РегистрРасчетаБаза.РегистраторРазрез КАК РегистраторРазрез,
	|	РегистрРасчетаБаза.НомерСтрокиРазрез КАК НомерСтрокиРазрез
	|ПОМЕСТИТЬ ВТНачисленияБаза
	|ИЗ
	|	&РегистрРасчетаБаза КАК РегистрРасчетаБаза";
	ЗапросРезультат.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	ЗапросРезультат.УстановитьПараметр("РегистрРасчетаБаза", Результат);
	ЗапросРезультат.Выполнить();

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияБаза.Сотрудник КАК Сотрудник,
	|	НачисленияБаза.ВидРасчета КАК ВидРасчета,
	|	НачисленияБаза.ПериодДействия КАК ПериодДействия,
	|	НачисленияБаза.Результат КАК Результат,
	|	НачисленияБаза.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
	|	НачисленияБаза.РегистраторРазрез КАК РегистраторРазрез,
	|	НачисленияБаза.НомерСтрокиРазрез КАК НомерСтрокиРазрез,
	|	НачисленияБаза.РезультатБаза КАК РезультатБаза
	|ИЗ
	|	ВТНачисленияБаза КАК НачисленияБаза";
	Возврат Запрос.Выполнить();
КонецФункции
