Функция ПолучитьДанные(СписокСотрудников, ПериодДействия, ФиксСторно)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
	Запрос.УстановитьПараметр("ПериодДействия", ПериодДействия);
	Запрос.УстановитьПараметр("ФиксСторно", ФиксСторно);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&ПериодДействия КАК ПериодДействия,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФильтрУдержанияБаза
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&СписокСотрудников)";
	Запрос.Выполнить();
	ЗапросПолученияДанныхРегистра = Новый Запрос;
	ЗапросПолученияДанныхРегистра.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	ЗапросПолученияДанныхРегистра.Текст = 
	"ВЫБРАТЬ
	|	РегистрРасчета.Регистратор КАК Регистратор,
	|	РегистрРасчета.НомерСтроки КАК НомерСтроки,
	|	РегистрРасчета.Сотрудник КАК Сотрудник,
	|	РегистрРасчета.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РегистрРасчета.Организация КАК Организация,
	|	РегистрРасчета.ВидРасчета КАК ВидРасчета,
	|	РегистрРасчета.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	РегистрРасчета.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	РегистрРасчета.Сторно КАК Сторно,
	|	РегистрРасчета.ПериодРегистрации КАК ПериодРегистрации,
	|	РегистрРасчета.Результат КАК Результат,
	|	НАЧАЛОПЕРИОДА(РегистрРасчета.БазовыйПериодНачало, МЕСЯЦ) КАК МесяцУдержания
	|ИЗ
	|	РегистрРасчета.Удержания КАК РегистрРасчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФильтрУдержанияБаза КАК Фильтр
	|		ПО (РегистрРасчета.ПериодРегистрации = Фильтр.ПериодДействия
	|				И РегистрРасчета.ФизическоеЛицо = Фильтр.ФизическоеЛицо
	|				И РегистрРасчета.ВидРасчета.ТребуетсяРасчетБазы)
	|ГДЕ
	|	РегистрРасчета.ФиксСторно = &ФиксСторно";
	ЗапросПолученияДанныхРегистра.УстановитьПараметр("ФиксСторно", ФиксСторно);

	ДанныеРегистра = ЗапросПолученияДанныхРегистра.Выполнить().Выгрузить();
	ОписаниеРассчитываемогоНабора = Обработки.РасчетБазыНачисленийУдержаний.НовыйОписаниеРассчитываемогоНабораУдержанийТаблица();
	Для Каждого СтрокаРегистра Из ДанныеРегистра Цикл
		СтрокаРассчитываемогоНабора = ОписаниеРассчитываемогоНабора.РассчитываемыеЗаписи.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРассчитываемогоНабора, СтрокаРегистра);
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	РасчетБазыНачислений = Обработки.РасчетБазыНачисленийУдержаний.Создать();
	УстановитьПривилегированныйРежим(Ложь);
	
	МенеджерРасчетаЗП = РасчетЗарплатыРасширенный.СоздатьМенеджерРасчета(
		КонецМесяца(ЗарплатаКадрыПериодическиеРегистры.МаксимальнаяДата()), 
		Справочники.Организации.ПустаяСсылка());

	НастройкиРасчетаБазы = Обработки.РасчетБазыНачисленийУдержаний.НовыйНастройкиРасчетаБазыУдержаний();
	РасчетБазыНачислений.ИнициализироватьРасчетБазыУдержаний(
		ОписаниеРассчитываемогоНабора, 
		МенеджерРасчетаЗП, 
		НастройкиРасчетаБазы);	
	РасчетнаяБаза = РасчетБазыНачислений.РасчетнаяБазаУдержаний();
	РасчетнаяБаза.Свернуть("ВидРасчетаРазрез,Регистратор,НомерСтроки","РезультатБаза, РезультатБазаВЦеломЗаМесяц");

	Результат = Новый ТаблицаЗначений;
	ИспользуемыеПоля = Новый Соответствие();
	ИспользуемыеПоля.Вставить("РЕЗУЛЬТАТБАЗАВЦЕЛОМЗАМЕСЯЦ", Истина);
	ИспользуемыеПоля.Вставить("ВИДРАСЧЕТАРАЗРЕЗ", Истина);
	ИспользуемыеПоля.Вставить("РЕЗУЛЬТАТ", Истина);
	ИспользуемыеПоля.Вставить("ПЕРИОДРЕГИСТРАЦИИ", Истина);
	ИспользуемыеПоля.Вставить("ВИДРАСЧЕТА", Истина);
	ИспользуемыеПоля.Вставить("РЕЗУЛЬТАТБАЗА", Истина);
	ИспользуемыеПоля.Вставить("СОТРУДНИК", Истина);
	Для Каждого Колонка Из ДанныеРегистра.Колонки Цикл
		Если ИспользуемыеПоля.Получить(ВРег(Колонка.Имя)) <> Неопределено Тогда
			Результат.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
		КонецЕсли;
	КонецЦикла;
	Для Каждого Колонка Из РасчетнаяБаза.Колонки Цикл
		Если ИспользуемыеПоля.Получить(ВРег(Колонка.Имя)) <> Неопределено Тогда
			Результат.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
		КонецЕсли;
	КонецЦикла;
	
	ДанныеРегистра.Индексы.Добавить("Регистратор, НомерСтроки");
	СтруктураПоиска = Новый Структура("Регистратор, НомерСтроки");
	
	Для Каждого СтрокаБазы Из РасчетнаяБаза Цикл
		СтрокаРезультат = Результат.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРезультат, СтрокаБазы);
		СтруктураПоиска.НомерСтроки = СтрокаБазы.НомерСтроки;
		СтруктураПоиска.Регистратор = СтрокаБазы.Регистратор;
		
		СтрокаДанныхРегистра = ДанныеРегистра.НайтиСтроки(СтруктураПоиска)[0];
		ЗаполнитьЗначенияСвойств(СтрокаРезультат, СтрокаДанныхРегистра);
	КонецЦикла;

	ЗапросРезультат = Новый Запрос();
	ЗапросРезультат.Текст = 
	"ВЫБРАТЬ
	|	РегистрРасчетаБаза.ПериодРегистрации КАК ПериодРегистрации,
	|	РегистрРасчетаБаза.ВидРасчета КАК ВидРасчета,
	|	РегистрРасчетаБаза.Результат КАК Результат,
	|	РегистрРасчетаБаза.Сотрудник КАК Сотрудник,
	|	РегистрРасчетаБаза.РезультатБаза КАК РезультатБаза,
	|	РегистрРасчетаБаза.РезультатБазаВЦеломЗаМесяц КАК РезультатБазаВЦеломЗаМесяц,
	|	РегистрРасчетаБаза.ВидРасчетаРазрез КАК ВидРасчетаРазрез
	|ПОМЕСТИТЬ ВТУдержанияБаза
	|ИЗ
	|	&РегистрРасчетаБаза КАК РегистрРасчетаБаза";
	ЗапросРезультат.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	ЗапросРезультат.УстановитьПараметр("РегистрРасчетаБаза", Результат);
	ЗапросРезультат.Выполнить();

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УдержанияБаза.Сотрудник КАК Сотрудник,
	|	УдержанияБаза.ВидРасчета КАК ВидРасчета,
	|	УдержанияБаза.ПериодРегистрации КАК ПериодРегистрации,
	|	УдержанияБаза.Результат КАК Результат,
	|	УдержанияБаза.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
	|	УдержанияБаза.РезультатБаза КАК РезультатБаза,
	|	УдержанияБаза.РезультатБазаВЦеломЗаМесяц КАК РезультатБазаВЦеломЗаМесяц
	|ИЗ
	|	ВТУдержанияБаза КАК УдержанияБаза";
	Возврат Запрос.Выполнить();
КонецФункции
