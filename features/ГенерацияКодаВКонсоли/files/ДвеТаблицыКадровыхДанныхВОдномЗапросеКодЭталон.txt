Функция ПолучитьДанные(Период, СписокСотрудников)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.Ссылка КАК Сотрудник,
	|	&Период КАК Период
	|ПОМЕСТИТЬ ВТФильтр
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&СписокСотрудников)";
	Запрос.Выполнить();
	Поля = Новый Массив();
	Поля.Добавить("ПЕРИОД");
	Поля.Добавить("ОСНОВНОЕНАЧИСЛЕНИЕ");
	Поля.Добавить("НАДБАВКА");
	Поля.Добавить("ТАРИФНАЯСТАВКА");
	Поля.Добавить("ВИДЗАНЯТОСТИ");
	Поля.Добавить("ВИДСОБЫТИЯ");
	Поля.Добавить("АВАНС");
	Поля.Добавить("СОТРУДНИК");
	ИмяВТФильтр = "ВТФильтр";
	ФормироватьСПериодичностьДень = Ложь;
	ИменаПолейФильтра = "Сотрудник,Период";
	ОписаниеФильтра = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц,
		ИмяВТФильтр,
		ИменаПолейФильтра);
	ОписаниеФильтра.ИмяВТКадровыеДанныеСотрудников = "ВТДанныеСотрудников";
	ТолькоРазрешенные = Ложь;
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(
		ОписаниеФильтра,
		ТолькоРазрешенные,
		Поля,,
		ФормироватьСПериодичностьДень);

	Поля = Новый Массив();
	Поля.Добавить("СОТРУДНИК");
	Поля.Добавить("ФАМИЛИЯИО");
	Поля.Добавить("ФОТ");
	ИмяВТФильтр = "ВТФильтр";
	ФормироватьСПериодичностьДень = Истина;
	ИменаПолейФильтра = "Сотрудник,Период";
	ОписаниеФильтра = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц,
		ИмяВТФильтр,
		ИменаПолейФильтра);
	ОписаниеФильтра.ИмяВТКадровыеДанныеСотрудников = "ВТДанныеСотрудниковДоп";
	ТолькоРазрешенные = Ложь;
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(
		ОписаниеФильтра,
		ТолькоРазрешенные,
		Поля,,
		ФормироватьСПериодичностьДень);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеСотрудников.Сотрудник КАК Сотрудник,
	|	ДанныеСотрудников.ВидСобытия КАК ВидСобытия,
	|	ДанныеСотрудников.ВидЗанятости КАК ВидЗанятости,
	|	ДанныеСотрудников.Аванс КАК Аванс,
	|	ДанныеСотрудников.ТарифнаяСтавка КАК ТарифнаяСтавка,
	|	ДанныеСотрудников.Надбавка КАК Надбавка,
	|	ДанныеСотрудников.ОсновноеНачисление КАК ОсновноеНачисление,
	|	ДанныеСотрудников.Период КАК Период,
	|	ДанныеСотрудниковДоп.ФОТ КАК ФОТ,
	|	ДанныеСотрудниковДоп.ФамилияИО КАК ФамилияИО
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	ВТДанныеСотрудников КАК ДанныеСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеСотрудниковДоп КАК ДанныеСотрудниковДоп
	|		ПО ДанныеСотрудников.Сотрудник = ДанныеСотрудниковДоп.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТКадровыеДанные.Сотрудник КАК Сотрудник,
	|	ВТКадровыеДанные.Аванс КАК Аванс,
	|	ВТКадровыеДанные.ВидЗанятости КАК ВидЗанятости,
	|	ВТКадровыеДанные.ОсновноеНачисление КАК ОсновноеНачисление,
	|	ВТКадровыеДанные.ФамилияИО КАК ФамилияИО,
	|	ВТКадровыеДанные.ФОТ КАК ФОТ,
	|	ВТКадровыеДанные.Период КАК Период
	|ИЗ
	|	ВТ КАК ВТКадровыеДанные";
	Возврат Запрос.Выполнить();
КонецФункции
