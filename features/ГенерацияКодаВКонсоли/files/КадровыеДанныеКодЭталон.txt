Функция ПолучитьДанные(Период, СписокСотрудников)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.Ссылка КАК Сотрудник,
	|	&Период КАК Период
	|ПОМЕСТИТЬ ВТФильтрДанныеСотрудников
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&СписокСотрудников)";
	Запрос.Выполнить();
	Поля = Новый Массив();
	Поля.Добавить("ОСНОВНОЕНАЧИСЛЕНИЕ");
	Поля.Добавить("НАДБАВКА");
	Поля.Добавить("ТАРИФНАЯСТАВКА");
	Поля.Добавить("ВИДЗАНЯТОСТИ");
	Поля.Добавить("ВИДСОБЫТИЯ");
	Поля.Добавить("АВАНС");
	Поля.Добавить("СОТРУДНИК");
	ИмяВТФильтр = "ВТФильтрДанныеСотрудников";
	ФормироватьСПериодичностьДень = Истина;
	ИменаПолейФильтра = "Сотрудник,Период";
	ОписаниеФильтра = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц,
		ИмяВТФильтр,
		ИменаПолейФильтра);
	ОписаниеФильтра.ИмяВТКадровыеДанныеСотрудников = "ВТДанныеСотрудников";
	ТолькоРазрешенные = Ложь;
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(
		ОписаниеФильтра,
		ТолькоРазрешенные,
		Поля,,
		ФормироватьСПериодичностьДень);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеСотрудников.Сотрудник КАК Сотрудник,
	|	ДанныеСотрудников.ВидСобытия КАК ВидСобытия,
	|	ДанныеСотрудников.ВидЗанятости КАК ВидЗанятости,
	|	ДанныеСотрудников.Аванс КАК Аванс,
	|	ДанныеСотрудников.ТарифнаяСтавка КАК ТарифнаяСтавка,
	|	ДанныеСотрудников.Надбавка КАК Надбавка,
	|	ДанныеСотрудников.ОсновноеНачисление КАК ОсновноеНачисление
	|ИЗ
	|	ВТДанныеСотрудников КАК ДанныеСотрудников";
	Возврат Запрос.Выполнить();
КонецФункции
