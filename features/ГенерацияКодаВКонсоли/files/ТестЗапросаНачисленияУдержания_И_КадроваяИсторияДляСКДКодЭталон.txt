Функция ПолучитьДанные(НачалоПериода, ОкончаниеПериода, Организация, СписокСотр)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СписокСотр", СписокСотр);
	ТекстЗапроса = ЗарплатаКадрыОбщиеНаборыДанныхПовтИсп.ПолучитьТекстОбщегоЗапроса(
		"НачисленияУдержания", 
		Ложь);
	
	СКД = Новый СхемаКомпоновкиДанных();
	Источник = СКД.ИсточникиДанных.Добавить();
	Источник.Имя = "Источник";
	Источник.ТипИсточникаДанных = "local";
	НаборСКД = СКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборСКД.ИсточникДанных = "Источник";
	НаборСКД.Запрос = ТекстЗапроса;
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьПредставленияЗапросов(СКД.НаборыДанных, СКД);
	
	ЗапросНачисленияУдержания = Новый Запрос(НаборСКД.Запрос);
	ЗапросНачисленияУдержания.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;

	НачалоПериода = НачалоПериода;
	ОкончаниеПериода = ОкончаниеПериода;
	ВыводитьОсновнойПоказательНачисленийИУдержаний = Истина;
	ВыводитьВсеПоказателиНачисленийИУдержаний = Истина;
	ВыводитьПериодыНачислений = Истина;
	ДетализироватьНачисленияПоСреднемуЗаработку = Ложь;

	ЗапросНачисленияУдержания.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ЗапросНачисленияУдержания.УстановитьПараметр("КонецПериода", ОкончаниеПериода);
	ЗапросНачисленияУдержания.УстановитьПараметр("ВыводитьПоказателиНачисленийИУдержаний", ВыводитьОсновнойПоказательНачисленийИУдержаний);
	ЗапросНачисленияУдержания.УстановитьПараметр("ВыводитьВсеПоказателиНачисленийИУдержаний", ВыводитьВсеПоказателиНачисленийИУдержаний);
	ЗапросНачисленияУдержания.УстановитьПараметр("ВыводитьПериодыНачислений", ВыводитьПериодыНачислений);
	ЗапросНачисленияУдержания.УстановитьПараметр("ИсключаемыеСсылки", Новый Массив());
	ЗапросНачисленияУдержания.УстановитьПараметр("ДетализироватьНачисленияПоСреднемуЗаработку", ДетализироватьНачисленияПоСреднемуЗаработку);
	
	Для Каждого Параметр Из СКД.Параметры Цикл
		ЗапросНачисленияУдержания.УстановитьПараметр(Параметр.Имя, Параметр.Значение);
	КонецЦикла;
	
	// установим отборы
	
	Схема = Новый СхемаЗапроса();
	Схема.УстановитьТекстЗапроса(ЗапросНачисленияУдержания.Текст);
	Для Каждого ЗапросПакета Из Схема.ПакетЗапросов Цикл
		Если ТипЗнч(ЗапросПакета) = Тип("ЗапросВыбораСхемыЗапроса")
			И ЗапросПакета.ТаблицаДляПомещения = "ОбщиеЗапросы_НачисленияУдержания" Тогда
				
			ЗапросПакета.ТаблицаДляПомещения = "";
			Колонки = ЗапросПакета.Колонки;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	НаборСКД.Запрос = Схема.ПолучитьТекстЗапроса();
	
	НастройкиСКД = СКД.ВариантыНастроек.Добавить().Настройки;
	Группа = НастройкиСКД.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));

	ИспользуемыеПоля = Новый Соответствие();
	ИспользуемыеПоля.Вставить("ПОРЯДОКПОКАЗАТЕЛЕЙ", Истина);
	ИспользуемыеПоля.Вставить("СТОРНО", Истина);
	ИспользуемыеПоля.Вставить("ПОКАЗАТЕЛЬ", Истина);
	ИспользуемыеПоля.Вставить("ВРЕМЯВЧАСАХ", Истина);
	ИспользуемыеПоля.Вставить("ОТРАБОТАНОДНЕЙВПЕРИОДЕ", Истина);
	ИспользуемыеПоля.Вставить("ОТРАБОТАНОДНЕЙ", Истина);
	ИспользуемыеПоля.Вставить("ДОКУМЕНТОСНОВАНИЕ", Истина);
	ИспользуемыеПоля.Вставить("ВИДРАСЧЕТА", Истина);
	ИспользуемыеПоля.Вставить("ДАТАОКОНЧАНИЯ", Истина);
	ИспользуемыеПоля.Вставить("ГРУППА", Истина);
	ИспользуемыеПоля.Вставить("ПОДРАЗДЕЛЕНИЕ", Истина);
	ИспользуемыеПоля.Вставить("РЕГИСТРАТОР", Истина);
	ИспользуемыеПоля.Вставить("ЗНАЧЕНИЕ", Истина);
	ИспользуемыеПоля.Вставить("ПЕРИОДДЕЙСТВИЯ", Истина);
	ИспользуемыеПоля.Вставить("ОТРАБОТАНОЧАСОВ", Истина);
	ИспользуемыеПоля.Вставить("ДАТАНАЧАЛА", Истина);
	ИспользуемыеПоля.Вставить("ОТРАБОТАНОЧАСОВВПЕРИОДЕ", Истина);
	ИспользуемыеПоля.Вставить("МЕСЯЦНАЧИСЛЕНИЯ", Истина);
	ИспользуемыеПоля.Вставить("ОПЛАЧЕНОЧАСОВ", Истина);
	ИспользуемыеПоля.Вставить("ОРГАНИЗАЦИЯ", Истина);
	ИспользуемыеПоля.Вставить("ГОЛОВНОЙСОТРУДНИК", Истина);
	ИспользуемыеПоля.Вставить("СУММА", Истина);
	ИспользуемыеПоля.Вставить("ОПЛАЧЕНОДНЕЙ", Истина);
	ИспользуемыеПоля.Вставить("СОТРУДНИК", Истина);
	ИспользуемыеПоля.Вставить("ФИЗИЧЕСКОЕЛИЦО", Истина);

	Для Каждого Колонка Из Колонки Цикл
		Если ИспользуемыеПоля.Получить(ВРег(Колонка.Псевдоним)) <> Неопределено Тогда
			Поле = Группа.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			Поле.Поле = Новый ПолеКомпоновкиДанных(Колонка.Псевдоним);
			Поле.Использование = Истина;
		КонецЕсли;
	КонецЦикла;

	Отбор = НастройкиСКД.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));	
	Отбор.Использование = Истина;
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
	Отбор.ПравоеЗначение = Организация;

	Отбор = НастройкиСКД.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));	
	Отбор.Использование = Истина;
	Если ТипЗнч(СписокСотр) = Тип("СправочникСсылка.Сотрудники") Тогда
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Иначе
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;	
	КонецЕсли;
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Сотрудник");
	Отбор.ПравоеЗначение = СписокСотр;

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СКД, НастройкиСКД);	
	
	Схема.УстановитьТекстЗапроса(МакетКомпоновкиДанных.НаборыДанных[0].Запрос);
	Для Каждого ЗапросПакета Из Схема.ПакетЗапросов Цикл
		Если ТипЗнч(ЗапросПакета) = Тип("ЗапросВыбораСхемыЗапроса")
			И Не ЗначениеЗаполнено(ЗапросПакета.ТаблицаДляПомещения) Тогда
			
			ЗапросПакета.ТаблицаДляПомещения = "ВТНачисленияУдержанияВыплаты";
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ЗапросНачисленияУдержания.Текст = Схема.ПолучитьТекстЗапроса();
	Для Каждого Параметр Из МакетКомпоновкиДанных.ЗначенияПараметров Цикл
		ЗапросНачисленияУдержания.УстановитьПараметр(Параметр.Имя, Параметр.Значение);	
	КонецЦикла;
		
	ЗапросНачисленияУдержания.Выполнить();

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТНачисленияУдержанияВыплаты.Сотрудник КАК Сотрудник,
	|	ВТНачисленияУдержанияВыплаты.МесяцНачисления КАК МесяцНачисления
	|ПОМЕСТИТЬ ВТФильтрКадроваяИсторияСотрудниковСрезПоследних
	|ИЗ
	|	ВТНачисленияУдержанияВыплаты КАК ВТНачисленияУдержанияВыплаты";
	Запрос.Выполнить();
	Измерения = Новый Массив();
	СоответствиеИзмерений = Новый Соответствие();
	СоответствиеИзмерений.Вставить("Период", "МесяцНачисления");
	Измерения.Добавить("Сотрудник");
	СоответствиеИзмерений.Вставить("Сотрудник", "Сотрудник");

	ОписаниеВТФильтр = ЗарплатаКадрыПериодическиеРегистры.ОписаниеФильтраДляСоздатьВТИмяРегистраПоВременнойТаблице(
		"ВТФильтрКадроваяИсторияСотрудниковСрезПоследних",
		Измерения);
	ОписаниеВТФильтр.СоответствиеИзмеренийРегистраИзмерениямФильтра = СоответствиеИзмерений;
	ПараметрыПостроения = ЗарплатаКадрыПериодическиеРегистры.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();

	ПараметрыПостроения.ИндексироватьПо = Новый Массив;





	ЗарплатаКадрыПериодическиеРегистры.СоздатьВТИмяРегистраСрезПоследних(
		"КадроваяИсторияСотрудников",
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		ОписаниеВТФильтр,
		ПараметрыПостроения,
		"ВТКадроваяИсторияСотрудниковСрезПоследних");

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТНачисленияУдержанияВыплаты.Сотрудник КАК Сотрудник,
	|	ВТНачисленияУдержанияВыплаты.МесяцНачисления КАК МесяцНачисления,
	|	ВТНачисленияУдержанияВыплаты.ВидРасчета КАК ВидРасчета,
	|	ВТНачисленияУдержанияВыплаты.Сумма КАК Сумма,
	|	ВТНачисленияУдержанияВыплаты.ОтработаноДней КАК ОтработаноДней,
	|	ВТНачисленияУдержанияВыплаты.ОтработаноЧасов КАК ОтработаноЧасов,
	|	ВТНачисленияУдержанияВыплаты.Сумма КАК Сумма1,
	|	ВТНачисленияУдержанияВыплаты.Показатель КАК Показатель,
	|	КадроваяИсторияСотрудниковСрезПоследних.ДолжностьПоШтатномуРасписанию КАК ДолжностьПоШтатномуРасписанию,
	|	КадроваяИсторияСотрудниковСрезПоследних.Должность КАК Должность
	|ИЗ
	|	ВТНачисленияУдержанияВыплаты КАК ВТНачисленияУдержанияВыплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадроваяИсторияСотрудниковСрезПоследних КАК КадроваяИсторияСотрудниковСрезПоследних
	|		ПО ВТНачисленияУдержанияВыплаты.Сотрудник = КадроваяИсторияСотрудниковСрезПоследних.Сотрудник
	|			И ВТНачисленияУдержанияВыплаты.МесяцНачисления = КадроваяИсторияСотрудниковСрезПоследних.Период";
	Возврат Запрос.Выполнить();
КонецФункции
