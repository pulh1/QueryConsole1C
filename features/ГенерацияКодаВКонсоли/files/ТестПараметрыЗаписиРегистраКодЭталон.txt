Функция ПолучитьДанные(ДатаНачала, Подразделение, ДатаОкончания)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&ДатаНачала КАК ДатаНачала,
	|	&ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТФильтрКадроваяИсторияСотрудниковЗаписи";
	Запрос.Выполнить();
	Измерения = Новый Массив();
	СоответствиеИзмерений = Новый Соответствие();
	Измерения.Добавить("ДатаОкончания");
	СоответствиеИзмерений.Вставить("ДатаОкончания", "ДатаОкончания");
	Измерения.Добавить("ДатаНачала");
	СоответствиеИзмерений.Вставить("ДатаНачала", "ДатаНачала");
	ОписаниеВТФильтр = ЗарплатаКадрыПериодическиеРегистры.ОписаниеФильтраДляСоздатьВТИмяРегистраПоВременнойТаблице(
		"ВТФильтрКадроваяИсторияСотрудниковЗаписи",
		Измерения);
	ОписаниеВТФильтр.СоответствиеИзмеренийРегистраИзмерениямФильтра = СоответствиеИзмерений;
	ПараметрыПостроения = ЗарплатаКадрыПериодическиеРегистры.ПараметрыПостроенияДляСоздатьВТИмяРегистра();

	ПараметрыПостроения.ИндексироватьПо = Новый Массив;
	ПараметрыПостроения.ИндексироватьПо.Добавить("Сотрудник");
	ПараметрыПостроения.ИндексироватьПо.Добавить("Организация");


	ПараметрыПостроения.ФормироватьСПериодичностьДень = Ложь;
	ПараметрыПостроения.ВключатьЗаписиНаНачалоПериода = Ложь;
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "Подразделение", "В", Подразделение);


	ЗарплатаКадрыПериодическиеРегистры.СоздатьВТИмяРегистра(
		"КадроваяИсторияСотрудников",
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		ОписаниеВТФильтр,
		ПараметрыПостроения,
		"ВТКадроаяИстория");

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТКадроаяИстория.Сотрудник КАК Сотрудник,
	|	ВТКадроаяИстория.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ВТКадроаяИстория.Подразделение КАК Подразделение,
	|	ВТКадроаяИстория.Должность КАК Должность,
	|	ФизическиеЛица.ИНН КАК ИНН,
	|	ФизическиеЛица.Пол КАК Пол,
	|	ФизическиеЛица.ФИО КАК ФИО,
	|	ВТКадроаяИстория.Регистратор КАК Регистратор,
	|	ВТКадроаяИстория.ПериодЗаписи КАК ПериодЗаписи
	|ИЗ
	|	ВТКадроаяИстория КАК ВТКадроаяИстория
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО ВТКадроаяИстория.Сотрудник.ФизическоеЛицо = ФизическиеЛица.Ссылка";
	Возврат Запрос.Выполнить();
КонецФункции
