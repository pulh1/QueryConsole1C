Функция ПолучитьДанные(Подразделение, СписокСотрудников, ДатаОкончания, ДатаНачала)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.Ссылка КАК Сотрудник,
	|	&ДатаНачала КАК ДатаНачала,
	|	&ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТФильтрДанныеУчетаВремениСотрудников
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&СписокСотрудников)";
	Запрос.Выполнить();
	ПараметрыПолученияДанных = УчетРабочегоВремениРасширенный.ПараметрыДляЗапросВТДанныеУчетаВремениИСостоянийСотрудников();
	ПараметрыПолученияДанных.ИмяВТСотрудники = "ВТФильтрДанныеУчетаВремениСотрудников";
	ПараметрыПолученияДанных.ИмяВТРезультат = "ВТДанныеУчетаВремениСотрудников";
	ПараметрыПолученияДанных.РассчитыватьПлановоеВремя = Истина;

	ПараметрыПолученияДанных.Подразделение = Подразделение;
	ПараметрыПолученияДанных.ДатаНачала = ДатаНачала;
	ПараметрыПолученияДанных.МесяцДатаНачала = НачалоМесяца(ПараметрыПолученияДанных.ДатаНачала);
	ПараметрыПолученияДанных.ДатаОкончания = ДатаОкончания;
	ПараметрыПолученияДанных.МесяцДатаОкончания = КонецМесяца(ПараметрыПолученияДанных.ДатаОкончания);
	ПараметрыПолученияДанных.ДатаАктуальности = ДатаНачала;
	ПараметрыПолученияДанных.ПолучатьУсловияТрудаИТерритории = Истина;
	ПараметрыПолученияДанных.ВыделятьВыходныеВПериодыОтклонений = Ложь;
	ПараметрыПолученияДанных.УчитыватьТабельныеДанныеОТерриториях = Ложь;
	ПараметрыПолученияДанных.ОтноситьПереходящуюЧастьСменыКДнюНачала = Истина;
	УчетРабочегоВремениРасширенный.СоздатьВТДанныеУчетаВремениИСостоянийСотрудников(
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		ПараметрыПолученияДанных);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеУчетаВремениСотрудников.Сотрудник КАК Сотрудник,
	|	ДанныеУчетаВремениСотрудников.Организация КАК Организация,
	|	ДанныеУчетаВремениСотрудников.Подразделение КАК Подразделение,
	|	ДанныеУчетаВремениСотрудников.Должность КАК Должность,
	|	ДанныеУчетаВремениСотрудников.Дата КАК Дата,
	|	ДанныеУчетаВремениСотрудников.ВидУчетаВремени КАК ВидУчетаВремени,
	|	ДанныеУчетаВремениСотрудников.Дни КАК Дни,
	|	ДанныеУчетаВремениСотрудников.Часы КАК Часы,
	|	ДанныеУчетаВремениСотрудников.Отклонение КАК Отклонение,
	|	ДанныеУчетаВремениСотрудников.Смена КАК Смена,
	|	ДанныеУчетаВремениСотрудников.Территория КАК Территория,
	|	ДанныеУчетаВремениСотрудников.УсловияТруда КАК УсловияТруда,
	|	ДанныеУчетаВремениСотрудников.НормаЧасов КАК НормаЧасов,
	|	ДанныеУчетаВремениСотрудников.ПереходящаяЧастьПредыдущейСмены КАК ПереходящаяЧастьПредыдущейСмены,
	|	ДанныеУчетаВремениСотрудников.ПереходящаяЧастьТекущейСмены КАК ПереходящаяЧастьТекущейСмены
	|ИЗ
	|	ВТДанныеУчетаВремениСотрудников КАК ДанныеУчетаВремениСотрудников";
	Возврат Запрос.Выполнить();
КонецФункции
