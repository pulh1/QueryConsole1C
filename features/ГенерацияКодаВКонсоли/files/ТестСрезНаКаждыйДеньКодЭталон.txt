Функция ПолучитьДанные(Сотрудник, ДатаОкончания, ДатаНачала)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	НачалоИнтервала = ДатаНачала;
	ОкончаниеИнтервала = ДатаОкончания;
	Периодичность = "ДЕНЬ";
	ИспользоватьКонецПериода = Ложь;
	ИмяПоляПериод = "Период";

	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТПериоды(
		Запрос.МенеджерВременныхТаблиц, 
		НачалоИнтервала, 
		ОкончаниеИнтервала,
		Периодичность,
		ИмяПоляПериод, 
		"ВТПериоды", 
		ИспользоватьКонецПериода);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Периоды.Период КАК Период,
	|	Сотрудники.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТФильтрКадроваяИсторияСотрудниковСрезПоследних
	|ИЗ
	|	ВТПериоды КАК Периоды,
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка = &Сотрудник";
	Запрос.Выполнить();
	Измерения = Новый Массив();
	СоответствиеИзмерений = Новый Соответствие();
	Измерения.Добавить("Сотрудник");
	СоответствиеИзмерений.Вставить("Сотрудник", "Ссылка");
	СоответствиеИзмерений.Вставить("Период", "Период");

	ОписаниеВТФильтр = ЗарплатаКадрыПериодическиеРегистры.ОписаниеФильтраДляСоздатьВТИмяРегистраПоВременнойТаблице(
		"ВТФильтрКадроваяИсторияСотрудниковСрезПоследних",
		Измерения);
	ОписаниеВТФильтр.СоответствиеИзмеренийРегистраИзмерениямФильтра = СоответствиеИзмерений;
	ПараметрыПостроения = ЗарплатаКадрыПериодическиеРегистры.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();

	ПараметрыПостроения.ИндексироватьПо = Новый Массив;





	ЗарплатаКадрыПериодическиеРегистры.СоздатьВТИмяРегистраСрезПоследних(
		"КадроваяИсторияСотрудников",
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		ОписаниеВТФильтр,
		ПараметрыПостроения,
		"ВТКадроваяИсторияСотрудниковСрезПоследних");

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КадроваяИсторияСотрудниковСрезПоследних.Сотрудник КАК Сотрудник,
	|	КадроваяИсторияСотрудниковСрезПоследних.Подразделение КАК Подразделение,
	|	КадроваяИсторияСотрудниковСрезПоследних.Должность КАК Должность,
	|	КадроваяИсторияСотрудниковСрезПоследних.Период КАК Период
	|ИЗ
	|	ВТКадроваяИсторияСотрудниковСрезПоследних КАК КадроваяИсторияСотрудниковСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник,
	|	Период";
	Возврат Запрос.Выполнить();
КонецФункции
