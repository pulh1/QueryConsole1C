#language: ru

@tree
@ExportScenarios
@IgnoreOnCIMainBuild

Функционал: Определение ветки метаданных и имени объекта

Сценарий: Я по полному имени источника "ПолноеИмяИсточника" сохраняю имя ветки и путь к таблице в переменные "ИмяПеременнойВетка" и "ИмяПеременнойПутьКТаблице"
	И я запоминаю строку "ПолноеИмяИсточника" в переменную "ПеременнаяПолноеИмяИсточника"	
	И я запоминаю строку "ИмяПеременнойВетка" в переменную "ПеременнаяИмяПеременнойВетка"	
	И я запоминаю строку "ИмяПеременнойПутьКТаблице" в переменную "ПеременнаяИмяПеременнойПутьКТаблице"
	И я выполняю код встроенного языка
	"""bsl
		ПолноеИмяОбъекта = Ванесса.ПолучитьСохраненноеЗначениеИзКонтекста("ПеременнаяПолноеИмяИсточника");
		ЧастиИмени = СтрРазделить(ПолноеИмяОбъекта, ".");

		Результат = Новый Массив;

		ДоступныВиртуальныеТаблицы = Ложь;
		ЭтоИсполняемоеПредставление = Ложь;
		Если ЧастиИмени.Количество() = 1 Тогда
			ИмяВетки = "Временные таблицы";
			Результат.Добавить(ЧастиИмени[0]);
		ИначеЕсли ЧастиИмени[0] = "КритерийОтбора" Тогда
			ИмяВетки = "КритерииОтбора"; 
		ИначеЕсли ЧастиИмени[0] = "ПланОбмена" Тогда
			ИмяВетки = "ПланыОбмена"; 
		ИначеЕсли ЧастиИмени[0] = "Константа" Тогда
			ИмяВетки = "Константы"; 
		ИначеЕсли ЧастиИмени[0] = "Справочник" Тогда
			ИмяВетки = "Справочники";
		ИначеЕсли ЧастиИмени[0] = "Документ" Тогда
			ИмяВетки = "Документы";
		ИначеЕсли ЧастиИмени[0] = "ЖурналДокументов"  Тогда
			ИмяВетки ="ЖурналыДокументов";
		ИначеЕсли ЧастиИмени[0] = "Перечисление" Тогда
			ИмяВетки = "Перечисления";
		ИначеЕсли ЧастиИмени[0] =  "ПланВидовХарактеристик" Тогда
			ИмяВетки = "ПланыВидовХарактеристик";
		ИначеЕсли ЧастиИмени[0] = "ПланВидовРасчета" Тогда
			ИмяВетки = "ПланыВидовРасчета";
		ИначеЕсли ЧастиИмени[0] = "РегистрСведений" Тогда
			ИмяВетки = "РегистрыСведений";
			ДоступныВиртуальныеТаблицы = Истина;
		ИначеЕсли ЧастиИмени[0] = "РегистрНакопления" Тогда
			ИмяВетки = "РегистрыНакопления";
			ДоступныВиртуальныеТаблицы = Истина;
		ИначеЕсли ЧастиИмени[0] = "РегистрРасчета" Тогда
			ИмяВетки = "РегистрыРасчета";
			ДоступныВиртуальныеТаблицы = Истина;
		ИначеЕсли ЧастиИмени[0] = "БизнесПроцесс" Тогда
			ИмяВетки = "БизнесПроцессы";
		ИначеЕсли ЧастиИмени[0] = "Задача" Тогда
			ИмяВетки = "Задачи";
		ИначеЕсли ЧастиИмени[0] = "ИсполняемоеПредставление" Тогда
			ИмяВетки = "Представления";
			ЭтоИсполняемоеПредставление = Истина;
		КонецЕсли;

		ИсполняемыеПредставленияПоПодсистемам = Новый Соответствие;
		ИсполняемыеПредставленияПоПодсистемам.Вставить("ДанныеУчетаВремениСотрудников", "УчетВремени");
		ИсполняемыеПредставленияПоПодсистемам.Вставить("ПлановоеВремяСотрудников", "УчетВремени");
		ИсполняемыеПредставленияПоПодсистемам.Вставить("ДанныеФизическихЛиц", "КадровыйУчет");
		ИсполняемыеПредставленияПоПодсистемам.Вставить("ДанныеСотрудников", "КадровыйУчет");
		ИсполняемыеПредставленияПоПодсистемам.Вставить("СотрудникиОрганизации", "КадровыйУчет");
		ИсполняемыеПредставленияПоПодсистемам.Вставить("СтажиФизическихЛиц", "КадровыйУчет");
		ИсполняемыеПредставленияПоПодсистемам.Вставить("РабочиеМестаСотрудников", "КадровыйУчет");
		ИсполняемыеПредставленияПоПодсистемам.Вставить("Периоды", "Общее");
		ИсполняемыеПредставленияПоПодсистемам.Вставить("ОстаткиОтпусков", "Отпуска");
		ИсполняемыеПредставленияПоПодсистемам.Вставить("ФактическиеОтпускаСотрудников", "Отпуска");
		ИсполняемыеПредставленияПоПодсистемам.Вставить("НачисленияУдержанияВыплаты", "УчетНачисленнойЗарплаты");
		ИсполняемыеПредставленияПоПодсистемам.Вставить("ОплаченноеВремя", "УчетНачисленнойЗарплаты");
		ИсполняемыеПредставленияПоПодсистемам.Вставить("НачисленияУдержанияВыплатыАвансом", "УчетНачисленнойЗарплаты");
		ИсполняемыеПредставленияПоПодсистемам.Вставить("ДанныеПозицийШтатногоРасписания", "ШтатноеРасписание");

		Если ЧастиИмени.Количество() = 3 И ДоступныВиртуальныеТаблицы Тогда
			Результат.Добавить(ЧастиИмени[1] + ЧастиИмени[2]);
		Иначе
			Для Индекс = 1 По ЧастиИмени.ВГраница() Цикл
				Если ЭтоИсполняемоеПредставление
					И Индекс = 1 Тогда

					Если ЧастиИмени[Индекс] = "РегистрСведений" Тогда
						Результат.Добавить("РегистрыСведений");		
					ИначеЕсли ЧастиИмени[Индекс] = "РегистрРасчета" Тогда
						Результат.Добавить("РегистрыРасчета");
					ИначеЕсли ИсполняемыеПредставленияПоПодсистемам.Получить(ЧастиИмени[Индекс]) <> Неопределено Тогда
						Результат.Добавить(ИсполняемыеПредставленияПоПодсистемам.Получить(ЧастиИмени[Индекс]));
						Результат.Добавить(ЧастиИмени[Индекс]);	
					Иначе
						Результат.Добавить(ЧастиИмени[Индекс]);
					КонецЕсли;
				Иначе
					Результат.Добавить(ЧастиИмени[Индекс]);	
				КонецЕсли;
			КонецЦикла
		КонецЕсли;

		ИмяПеременной = Ванесса.ПолучитьСохраненноеЗначениеИзКонтекста("ПеременнаяИмяПеременнойВетка");
		Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременной, ИмяВетки);
		ИмяПеременной = Ванесса.ПолучитьСохраненноеЗначениеИзКонтекста("ПеременнаяИмяПеременнойПутьКТаблице");
		Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременной, Результат);
	"""	