#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем ГенераторТекстовВыражений;
Перем ТекстовыйДокумент;
	
Процедура УстановитьГенераторТекстовВыражений(Генератор) Экспорт
	ГенераторТекстовВыражений = Генератор;
КонецПроцедуры
	
Процедура УстановитьТекстовыйДокумент(УстанавливаемыйДокумент) Экспорт
	ТекстовыйДокумент = УстанавливаемыйДокумент;
КонецПроцедуры
	
// Сценарий создания пакета запроса в тек док.
// 
// Параметры:
//  ТекстовыйДокумент - ТекстовыйДокумент
//  ТекстЗапроса - Строка
Процедура СценарийСозданияПакетаЗапросаВТекДок(ТекстЗапроса, ТекушиеТабуляции = "") Экспорт
	ГенераторВыражений = ГенерацияТекстовЗапросов.СоздатьГенераторТекстовВыражений();
	УстановитьГенераторТекстовВыражений(ГенераторВыражений);
	
	МодельПакета = ОбработкаМоделиЗапроса.РазобратьЗапрос(ТекстЗапроса);
	Если МодельПакета.Элементы.Количество() = 1 Тогда
		Если МодельПакета.Элементы[0].Тип = "ЗапросВыбора" Тогда
			СценарийСозданияЗапросаВыбораВТекДок(МодельПакета.Элементы[0], ТекушиеТабуляции);
		Иначе
			// TODO
		КонецЕсли;
			
		Возврат;
	КонецЕсли;
	
	Если МодельПакета.Элементы[0].Тип = "ЗапросВыбора" Тогда
		Если ЗначениеЗаполнено(МодельПакета.Элементы[0].ТаблицаДляПомещения) Тогда
			Сценарий = "* Создание временной таблицы " + МодельПакета.Элементы[0].ТаблицаДляПомещения;		
		Иначе
			Сценарий = "* Создание запроса получения данных";	
		КонецЕсли;
		ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Сценарий);
		СценарийСозданияЗапросаВыбораВТекДок(МодельПакета.Элементы[0], ТекушиеТабуляции + Символы.Таб);
	Иначе
		
	КонецЕсли;
	
	Для Индекс = 1 По МодельПакета.Элементы.ВГраница() Цикл
		Если МодельПакета.Элементы[0].Тип = "ЗапросВыбора" Тогда
			Если ЗначениеЗаполнено(МодельПакета.Элементы[0].ТаблицаДляПомещения) Тогда
				Сценарий = "* Создание временной таблицы " + МодельПакета.Элементы[0].ТаблицаДляПомещения;		
			Иначе
				Сценарий = "* Создание запроса получения данных";	
			КонецЕсли;
			ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Сценарий);
			СценарийСозданияЗапросаВыбораВТекДок(МодельПакета.Элементы[0], ТекушиеТабуляции + Символы.Таб);
		Иначе
			
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура СценарийСозданияЗапросаВыбораВТекДок(МодельЗапроса, ТекушиеТабуляции = "") Экспорт
	ГенераторТекстовВыражений.УстановитьКолонки(МодельЗапроса.Колонки);
	
	Если МодельЗапроса.Операторы.Количество() = 1 Тогда
		СценарийСозданияОператораЗапросаВыбораВТекДок(МодельЗапроса.Операторы[0], ТекушиеТабуляции);
		Возврат;
	КонецЕсли;
	
	ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + "* Создание оператора запроса");
	СценарийСозданияОператораЗапросаВыбораВТекДок(МодельЗапроса.Операторы[0], ТекушиеТабуляции + Символы.Таб);
	
	Для Индекс = 1 По МодельЗапроса.Операторы.ВГраница() Цикл
		ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + "* Создание оператора запроса");
		Если МодельЗапроса.Операторы[Индекс].ТипОбъединения = ЭлементыМоделиЗапроса.НовыйТипОбъединение() Тогда
			ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + "И Я добавляю объединяемый запрос без дублирования записей");
		Иначе
			ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + "И Я добавляю объединяемый запрос");
		КонецЕсли;	
		СценарийСозданияОператораЗапросаВыбораВТекДок(МодельЗапроса.Операторы[Индекс], ТекушиеТабуляции + Символы.Таб);
	КонецЦикла;
	
КонецПроцедуры

Функция СоздатьГенераторДляВложенногоЗапроса() 
	УстановитьПривилегированныйРежим(Истина);
	Генератор = Обработки.ГенераторFeatureФайлов.Создать();
	УстановитьПривилегированныйРежим(Ложь);
	
	ГенераторВыражений = ГенерацияТекстовЗапросов.СоздатьГенераторТекстовВыражений();
	Генератор.УстановитьГенераторТекстовВыражений(ГенераторВыражений);
	
	Генератор.УстановитьТекстовыйДокумент(ТекстовыйДокумент);
	
	Возврат Генератор;
КонецФункции

Процедура СценарийСозданияОператораЗапросаВыбораВТекДок(МодельОператораЗапроса, Знач ТекушиеТабуляции = "")
	ГенераторТекстовВыражений.УстановитьИсточники(МодельОператораЗапроса.Источники.Элементы);
	
	ИсточникиЗапросаВТекстовыйДокумент(МодельОператораЗапроса.Источники.Элементы, ТекушиеТабуляции);
	
	ИсточникиПоИдентификаторам = МодельЗапросаУтилиты.МассивСтруктурВСоответствие(МодельОператораЗапроса.Источники.Элементы, "ИдентификаторИсточника");
	Для Каждого Источник Из МодельОператораЗапроса.Источники.Элементы Цикл
		СоединенияИсточникаВТекДок(Источник, ИсточникиПоИдентификаторам, ТекушиеТабуляции);	
	КонецЦикла;
	
	ПоляОператораВТекДок(МодельОператораЗапроса.ВыбираемыеПоля, ТекушиеТабуляции);
	
	ПоляГруппировкиВТекДок(МодельОператораЗапроса.Группировка.Элементы, МодельОператораЗапроса.ВыбираемыеПоля, ТекушиеТабуляции);
	УстановкаАгрегатрыхФункцийВТекДок(МодельОператораЗапроса.ВыбираемыеПоля, ТекушиеТабуляции);
	
	ОтборВТекДок(МодельОператораЗапроса.Отбор, ТекушиеТабуляции);
	ОтборВТекДок(МодельОператораЗапроса.ОтборСгруппированных, ТекушиеТабуляции);
КонецПроцедуры

Процедура ИсточникиЗапросаВТекстовыйДокумент(ИсточникиЗапроса, Знач ТекушиеТабуляции = "")
	Для Каждого ТекущийИсточник Из ИсточникиЗапроса Цикл
		Если ТекущийИсточник.Источник.Тип = "ИсточникДанныхТаблица"
			Или (ТекущийИсточник.Источник.Тип = "ИсточникДанныхТаблица" 
				И ТекущийИсточник.Источник.ОписаниеВТ.ИндексЗапросаСоздания <> -1) Тогда	
			
			Шаг = "И Я добавляю источник """ + ТекущийИсточник.Источник.ИмяТаблицы + """ как """ 
				+ ТекущийИсточник.Источник.Псевдоним + """ в конструкторе запроса";
			
			ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг);
		ИначеЕсли ТекущийИсточник.Источник.Тип = "ИсточникДанныхВременнаяТаблица" 
			Или ТекущийИсточник.Источник.Тип = "ИсточникДанныхТаблицаЗначений"  Тогда  
			
			ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции +  "И Я запоминаю в переменную ""ПоляВТ"" значение");
			ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции +  "|'Поле'|");
			Для Каждого Поле Из ТекущийИсточник.Источник.ОписаниеВТ.ПорядокКолонок Цикл
				ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции +  "|'" + Поле + "'|");
			КонецЦикла;
			Шаг = "И Я добавляю описание временной таблицы """ + ТекущийИсточник.Источник.ИмяТаблицы + """ как """ 
				+ ТекущийИсточник.Источник.Псевдоним + """ с полями ""$ПоляВТ$""";
			ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг);
		ИначеЕсли ТекущийИсточник.Источник.Тип = "ИсточникДанныхВложенныйЗапрос" Тогда 
			СценарийДобавленияВложенногоЗапросаВМассивСтрок(ТекущийИсточник.Источник, ТекушиеТабуляции);	
		ИначеЕсли ТекущийИсточник.Источник.Тип = "ИсполняемоеПредставление" Тогда 
			СценарийДобавленияИсполняемогоПредставленияВТекДок(ТекущийИсточник.Источник, ТекушиеТабуляции);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура СценарийДобавленияВложенногоЗапросаВМассивСтрок(Источник, Знач ТекушиеТабуляции = "")
	ТекстовыйДокумент.ДобавитьСтроку("* Вложенный запрос");	
	ТекушиеТабуляции = ТекушиеТабуляции + Символы.Таб;
	
	Генератор = СоздатьГенераторДляВложенногоЗапроса();
	Генератор.СценарийСозданияЗапросаВыбораВТекДок(Источник.ЗапросВыбора, ТекушиеТабуляции);
	
	Шаг = "И Я устанавливаю Псевдоним """ + Источник.Псевдоним + """ для текущего источника";
	ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг);
КонецПроцедуры

// Сценарий добавления исполняемого представления в массив строк.
// 
// Параметры:
//  ТекстовыйДокумент - ТекстовыйДокумент
//  Источник - см. ЭлементыМоделиЗапроса.НовыйИсполняемоеПредставление
//  ТекушиеТабуляции - Строка - Текушие табуляции
Процедура СценарийДобавленияИсполняемогоПредставленияВТекДок(Источник, Знач ТекушиеТабуляции = "")
	ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + "* Добавление " + Источник.ИмяТаблицы);	
	ТекушиеТабуляции = ТекушиеТабуляции + Символы.Таб;
	Шаг = "И Я добавляю источник """ + Источник.ИмяТаблицы + """ в конструкторе запроса";
	ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг);
	
	Для Каждого Параметр Из Источник.Параметры Цикл
		Если Параметр.ЭтоПараметрЗапроса Тогда
			Шаг = "И Я присваиваю параметр запроса """ + Параметр.Значение + """ параметру + """ + Параметр.Имя + """ исполняемого представления";
		Иначе
			Если ТипЗнч(Параметр.Значение) = Тип("Булево") Тогда
				Если Параметр.Значение Тогда
					Значение = "Истина";
				Иначе
					Значение = Ложь;
				КонецЕсли;
			ИначеЕсли ТипЗнч(Параметр.Значение) = Тип("Число") Тогда 
				Значение = Формат(Параметр.Значение, "ЧРД=.; ЧГ=;");
			Иначе
				Значение = Параметр.Значение;
			КонецЕсли;
			Шаг = "И Я присваиваю значение выражения '" + Значение + "' параметру """ + Параметр.Имя + """ исполняемого представления";
			ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг);
		КонецЕсли;
	КонецЦикла;
	
	Если Источник.ВТФильтр <> Неопределено Тогда
		ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + "* Установка запроса фильтра");	
		
		Генератор = СоздатьГенераторДляВложенногоЗапроса();
		Генератор.СценарийСозданияЗапросаВыбораВТекДок(Источник.ВТФильтр.ЗапросВыбора, ТекушиеТабуляции + Символы.Таб);
		
		Для Индекс = 0 По Источник.ВТФильтр.Поля.ВГраница() Цикл
			Если Источник.ВТФильтр.Поля[Индекс] <> Источник.ВТФильтр.ЗапросВыбора.Колонки[Индекс].Имя Тогда
				Шаг = "И Я устанавливаю соответствие поля """ + Источник.ВТФильтр.ЗапросВыбора.Колонки[Индекс].Имя 
					+ """ полю фильтра """ + Источник.ВТФильтр.Поля[Индекс] + """";	
				ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг);	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Шаг = "И Я устанавливаю Псевдоним """ + Источник.Псевдоним + """ для текущего источника";
	ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг);
КонецПроцедуры

Процедура СоединенияИсточникаВТекДок(Источник, ИсточникиПоИдентификаторам, Знач ТекушиеТабуляции = "")
	Для Каждого Соединение Из Источник.Соединения Цикл
		ВладелецСвязи = Источник.Источник.Псевдоним;
		ПрисоединяемаяТаблица = ИсточникиПоИдентификаторам[Соединение.Источник].Псевдоним;
		УсловиеСвязи = ГенерацияТекстовЗапросов.ВыражениеВСтроку(Соединение, ГенераторТекстовВыражений);
		ТипСоединения = Соединение.ТипСоединения;
		
		Шаг = "И Я устанавливаю """ + ТипСоединения + """ соединение источника """ 
			+ ВладелецСвязи + """ с источником """ + ПрисоединяемаяТаблица + """ по условию ";
			
		Если СтрЧислоСтрок(УсловиеСвязи) = 1 Тогда
			Шаг = Шаг + "'" + УсловиеСвязи + "'";
		Иначе
			ПрисвоениеМногострочновоВыраженияПеременнойВТекДок(УсловиеСвязи, "УсловиеСвязи", ТекушиеТабуляции);
			Шаг = Шаг + "'$УсловиеСвязи$'";
		КонецЕсли;
		
		ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг);
	КонецЦикла;
КонецПроцедуры

Процедура ПоляОператораВТекДок(ПоляОператора, ТекушиеТабуляции)
	Для Каждого Поле Из ПоляОператора Цикл
		АгрегатнаяФункция = Неопределено;
		Если Поле.Выражение.Значение.Тип = "АгрегатнаяФункция" Тогда
			ВыражениеПоля = Поле.Выражение.Значение.Аргумент;	
			АгрегатнаяФункция = Поле.Выражение.Значение.ИмяФункции;
		ИначеЕсли Поле.Выражение.Значение.Тип = "АгрегатнаяФункцияКоличество" Тогда
			ВыражениеПоля = Поле.Выражение.Значение.Аргумент;
			Если Поле.Выражение.Значение.Различные Тогда
				АгрегатнаяФункция = "КоличествоРазличных";
			Иначе
				АгрегатнаяФункция = "Количество";
			КонецЕсли;
		Иначе
			ВыражениеПоля = Поле.Выражение.Значение;
		КонецЕсли;
		ТекстВыражения = ГенерацияТекстовЗапросов.ВыражениеВСтроку(ВыражениеПоля, ГенераторТекстовВыражений);
		
		Если ЭтоПолеИсточника(ВыражениеПоля) Тогда
			Шаг = "И Я добавляю поле запроса """ + ТекстВыражения + """ как """ + Поле.Псевдоним + """ в конструкторе запроса";	
		Иначе
			Если СтрЧислоСтрок(ТекстВыражения) = 1 Тогда
				Шаг = "И Через редактор выражений я добавляю поле запроса '" + ТекстВыражения + "' как """ + Поле.Псевдоним + """";	
			Иначе
				ПрисвоениеМногострочновоВыраженияПеременнойВТекДок(ТекстВыражения, "ВыражениеПоля", ТекушиеТабуляции);
				Шаг = "И Через редактор выражений я добавляю поле запроса ""$ВыражениеПоля$"" как """ + Поле.Псевдоним + """";
			КонецЕсли;		
		КонецЕсли;
		ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг);
	КонецЦикла; 
КонецПроцедуры

Процедура ПоляГруппировкиВТекДок(ПоляГруппировки, ПоляЗапроса, ТекушиеТабуляции)
	ВыражениеПолей = Новый Соответствие();
	Для Каждого Поле Из ПоляЗапроса Цикл
		ВыражениеПолей.Вставить(ГенерацияТекстовЗапросов.ВыражениеВСтроку(Поле.Выражение, ГенераторТекстовВыражений), Истина);	
	КонецЦикла;
	
	Для Каждого ЭлементГруппировки Из ПоляГруппировки Цикл
		ВыражениеГруппировки = ГенерацияТекстовЗапросов.ВыражениеВСтроку(ЭлементГруппировки, ГенераторТекстовВыражений);
		Если ВыражениеПолей.Получить(ВыражениеГруппировки) <> Неопределено Тогда
			Если СтрЧислоСтрок(ВыражениеГруппировки) = 1 Тогда
				Шаг = "И Я добавляю группировку по выражению '" + ВыражениеГруппировки + "' поля запроса";
			Иначе
				Шаг = "И Я добавляю группировку по выражению ""$Выражение$"" поля запроса";
			КонецЕсли;	
			ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг); 	
			
			ВыражениеПолей.Удалить(ВыражениеГруппировки);
		ИначеЕсли ЭтоПолеИсточника(ЭлементГруппировки.Значение) Тогда 
			Шаг = "Я добавляю группировку по полю  источника '" + ВыражениеГруппировки + "'";
			ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг); 
		Иначе
			ПрисвоениеМногострочновоВыраженияПеременнойВТекДок(ВыражениеГруппировки, "ВыражениеПоля", ТекушиеТабуляции);
			Шаг = "И Через редактор выражений я добавляю поле запроса ""$ВыражениеПоля$""";	
			ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг); 	
			Шаг = "И Я добавляю группировку по выражению ""$ВыражениеПоля$"" поля запроса";
			ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг);
			Шаг = "И Я удаляю последнее поле оператора";
			ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг);	
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры	

Процедура ОтборВТекДок(ЭлементыОтбора, ТекушиеТабуляции)
	Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		ТекстВыражения = ГенерацияТекстовЗапросов.ВыражениеВСтроку(ЭлементОтбора, ГенераторТекстовВыражений);
		Если ЭтоОборПоПолюИсточника(ЭлементОтбора.Значение) Тогда
			Шаг = "И Я добавляю условие на поле источника '" + ТекстВыражения + "' в конструкторе запроса"	
		Иначе
			Если СтрЧислоСтрок(ТекстВыражения) = 1 Тогда
				Шаг = "И Через редактор выражений я добавляю условие '" + ТекстВыражения + "'";
			Иначе
				ПрисвоениеМногострочновоВыраженияПеременнойВТекДок(ТекстВыражения, "ВыражениеОтбора", ТекушиеТабуляции);
				Шаг = "И Через редактор выражений я добавляю условие '$ВыражениеОтбора$'";
			КонецЕсли;	
		КонецЕсли;
		ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг);
	КонецЦикла;	
КонецПроцедуры

Процедура УстановкаАгрегатрыхФункцийВТекДок(ПоляОператора, ТекушиеТабуляции)
	Для Каждого Поле Из ПоляОператора Цикл
		АгрегатнаяФункция = Неопределено;
		Если Поле.Выражение.Значение.Тип = "АгрегатнаяФункция" Тогда
			ВыражениеПоля = Поле.Выражение.Значение.Аргумент;	
			АгрегатнаяФункция = Поле.Выражение.Значение.ИмяФункции;
		ИначеЕсли Поле.Выражение.Значение.Тип = "АгрегатнаяФункцияКоличество" Тогда
			ВыражениеПоля = Поле.Выражение.Значение.Аргумент;
			Если Поле.Выражение.Значение.Различные Тогда
				АгрегатнаяФункция = "КоличествоРазличных";
			Иначе
				АгрегатнаяФункция = "Количество";
			КонецЕсли;
		Иначе
			Продолжить;
		КонецЕсли;
		ТекстВыражения = ГенерацияТекстовЗапросов.ВыражениеВСтроку(ВыражениеПоля, ГенераторТекстовВыражений);
		
		Если АгрегатнаяФункция <> Неопределено Тогда
			Если СтрЧислоСтрок(ТекстВыражения) = 1 Тогда
				Шаг = "И Я устанавливаю агрегатрую функцию """ + АгрегатнаяФункция + """ + для поля с выражением '" + ТекстВыражения + "'";
			Иначе
				Шаг = "И Я устанавливаю агрегатрую функцию """ + АгрегатнаяФункция + """ + для поля с выражением ""$ВыражениеПоля$""";
			КонецЕсли;	
			ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг);
		КонецЕсли;	
	КонецЦикла; 
КонецПроцедуры

Процедура ПрисвоениеМногострочновоВыраженияПеременнойВТекДок(Выражение, ИмяПеременнной, Знач ТекушиеТабуляции)
	Шаг = "И Я запоминаю в переменную """ + ИмяПеременнной + """ значение """;
	ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Шаг);
	Для НомерСтроки = 1 По СтрЧислоСтрок(Выражение) Цикл
		Строка = "|'" + СтрПолучитьСтроку(Выражение, НомерСтроки) + "'|";
		ТекстовыйДокумент.ДобавитьСтроку(ТекушиеТабуляции + Строка);
	КонецЦикла;
	ТекстовыйДокумент.ДобавитьСтроку("""");
КонецПроцедуры

Функция ЭтоОборПоПолюИсточника(Выражение)
	Если ЭтоПолеИсточника(Выражение) Тогда
		Возврат Истина;		
	КонецЕсли;
	
	Если Не (Выражение.Тип = "БинарнаяОперация"
		И Выражение.Операция = "="
		И ЭтоПолеИсточника(Выражение.ЛеваяЧасть)) Тогда
			
		Возврат Ложь;
	КонецЕсли;
	
	Если Выражение.ЛеваяЧасть.Тип = "ПолеИсточника" Тогда
		ИмяПоля = Выражение.ЛеваяЧасть.ИмяПоля;
	Иначе
		ИмяПоля = Выражение.ЛеваяЧасть.Элементы[Выражение.ЛеваяЧасть.Элементы.ВГраница()];
	КонецЕсли;
	
	Если ТипЗнч(Выражение.ПраваяЧасть) = Тип("Структура")
		И Выражение.ПраваяЧасть.Тип = "ПараметрЗапроса"
		И Выражение.ПраваяЧасть.Имя = ИмяПоля Тогда
			
		Возврат Истина;
	КонецЕсли;
		
	Возврат Ложь;
КонецФункции

Функция ЭтоПолеИсточника(Выражение)
	Если ТипЗнч(Выражение) <> Тип("Структура") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Выражение.Тип <> "Разыменование" Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Выражение.Элементы[0].Тип <> "ПолеИсточника" Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Индекс = 1 По Выражение.Элементы.ВГраница() Цикл
		Если ТипЗнч(Выражение.Элементы[Индекс]) <> Тип("Строка") Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

#КонецЕсли