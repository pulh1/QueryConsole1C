
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции

Функция Описание(ИмяРегистра) Экспорт
	МетаданныеРегистра = Метаданные.РегистрыСведений.Найти(ИмяРегистра);
	
	Если МетаданныеРегистра = Неопределено
		Или МетаданныеРегистра.ПериодичностьРегистраСведений = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
			
		Возврат Неопределено;
	КонецЕсли;
	
	Описание = ЭлементыМоделиОписанияПредставлений.НовыйОписаниеПредставления();
	Описание.Имя = ИмяПредставления(ИмяРегистра);
	
	ПоляРегистра = ПредставленияРегистровСведенийУтилиты.ДоступныеПоляРегистраСведений(МетаданныеРегистра);
  
	Описание.ПоддерживаютсяИндексы = Истина; 
	Описание.ПоддерживаетсяУказаниеИмяВТРезультат = Истина;  
	Описание.ПоддерживаетсяПолучениеРезультатаЗапроса = Истина;;
	Описание.ПоддерживаетсяПсевдонимыПолей = Истина;
	Описание.ДоступноВМеханизмеПредставленийСКД = Истина;
	Описание.ИмяВТДляСКД = "Представления_СрезПоследних_" + ИмяРегистра;
		
	Для Каждого Поле Из ПоляРегистра Цикл
		Описание.Поля.Добавить(Поле);
	КонецЦикла;	
	
	Поле = ЭлементыМоделиОписанияПредставлений.НовыйПолеПредставления();
	Поле.Имя = "ПериодЗаписи";
	Поле.ТипЗначения = Новый ОписаниеТипов("Дата"); 
	Описание.Поля.Добавить(Поле);
	
	Поле = ЭлементыМоделиОписанияПредставлений.НовыйПолеПредставления();
	Поле.Имя = "Период";
	Поле.ТипЗначения = Новый ОписаниеТипов("Дата"); 
	Описание.Поля.Добавить(Поле);
	
	Если МетаданныеРегистра.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору Тогда
		Поле = ЭлементыМоделиОписанияПредставлений.НовыйПолеПредставления();
		Поле.Имя = "Регистратор";
		Поле.ТипЗначения = МетаданныеРегистра.СтандартныеРеквизиты.Регистратор.Тип; 
		Описание.Поля.Добавить(Поле);
	КонецЕсли;
	
	ПоляФильтра = ПредставленияРегистровСведенийУтилиты.ИзмеренияРегистраСведений(МетаданныеРегистра);  
	Поле = ЭлементыМоделиОписанияПредставлений.НовыйПолеФильтра(); 
	Поле.Имя = "Период"; 
	Поле.Обязательный = Истина;

	ПоляФильтра.Вставить(0, Поле);
	
	Описание.ОписаниеВТФильтр = ЭлементыМоделиОписанияПредставлений.НовыйОписаниеВТФильтр(); 
	
	Описание.ОписаниеВТФильтр.Обязательный = Истина; 
	
	Описание.ОписаниеВТФильтр.ПоддерживаютсяПсевдонимы = Истина;
	Описание.ОписаниеВТФильтр.ПоддерживаетсяИмяВТФильтр = Истина;   
	Описание.ОписаниеВТФильтр.ПоддерживаетсяТЗФильтр = Истина; 	 
	Описание.ОписаниеВТФильтр.ПоляФильтра = ПоляФильтра;
	
	ПараметрФормироватьСПериодичностьДень = ЭлементыМоделиОписанияПредставлений.НовыйОписаниеПараметраКонстанта();
	Описание.ОписаниеПараметров.Добавить(ПараметрФормироватьСПериодичностьДень);

	ПараметрФормироватьСПериодичностьДень.Обязательный = Ложь;
	ПараметрФормироватьСПериодичностьДень.Имя = "ФормироватьСПериодичностьДень"; 
	ПараметрФормироватьСПериодичностьДень.ДопустимПараметрЗапроса = Ложь;  
	ПараметрФормироватьСПериодичностьДень.ТипКонстанты = Новый ОписаниеТипов("Булево");
	ПараметрФормироватьСПериодичностьДень.ЗначениеПоУмолчанию = Истина;
	
	ПараметрВсеЗаписи = ЭлементыМоделиОписанияПредставлений.НовыйОписаниеПараметраКонстанта();
	Описание.ОписаниеПараметров.Добавить(ПараметрВсеЗаписи);

	ПараметрВсеЗаписи.Обязательный = Ложь;
	ПараметрВсеЗаписи.Имя = "ВсеЗаписи"; 
	ПараметрВсеЗаписи.ДопустимПараметрЗапроса = Ложь;  
	ПараметрВсеЗаписи.ТипКонстанты = Новый ОписаниеТипов("Булево");
	ПараметрВсеЗаписи.ЗначениеПоУмолчанию = Ложь;
	
	ПараметрВключаяГраницу = ЭлементыМоделиОписанияПредставлений.НовыйОписаниеПараметраКонстанта();
	Описание.ОписаниеПараметров.Добавить(ПараметрВключаяГраницу);

	ПараметрВключаяГраницу.Обязательный = Ложь;
	ПараметрВключаяГраницу.Имя = "ВключаяГраницу"; 
	ПараметрВключаяГраницу.ДопустимПараметрЗапроса = Ложь;  
	ПараметрВключаяГраницу.ТипКонстанты = Новый ОписаниеТипов("Булево");
	ПараметрВключаяГраницу.ЗначениеПоУмолчанию = Истина;
		
	ДоступенИнтервальныйРегистр = ПредставленияРегистровСведенийУтилиты.ДоступенИнтервальныйРегистрСведений(ИмяРегистра);
		
	Для Каждого Ресурс Из МетаданныеРегистра.Ресурсы Цикл
		ОписаниеОтбора = ЭлементыМоделиОписанияПредставлений.НовыйОписаниеОтбора();
		ОписаниеОтбора.Поле = Ресурс.Имя;
		ОписаниеОтбора.ДоступенДляСКД = ДоступенИнтервальныйРегистр;
		ОписаниеОтбора.ДоступноРазыменование = Истина;
		
		Описание.ДоступныеОтборы.Добавить(ОписаниеОтбора);	
	КонецЦикла;
	
	Для Каждого Реквизит Из МетаданныеРегистра.Реквизиты Цикл
		ОписаниеОтбора = ЭлементыМоделиОписанияПредставлений.НовыйОписаниеОтбора();
		ОписаниеОтбора.Поле = Реквизит.Имя;
		ОписаниеОтбора.ДоступенДляСКД = ДоступенИнтервальныйРегистр;
		ОписаниеОтбора.ДоступноРазыменование = Истина;;
		
		Описание.ДоступныеОтборы.Добавить(ОписаниеОтбора);	
	КонецЦикла;
			
	Возврат Описание;
КонецФункции

Функция Справка() Экспорт
	Справка = ЭлементыМоделиОписанияПредставлений.НовыйСправка();	
	Справка.Имя = "ИсполняемоеПредставление.РегистрСведений.ИмяРегистра.СрезПоследних";
	Справка.Описание = "Обеспечивает доступ к методу ""ЗарплатаКадрыПериодическиеРегистры.СоздатьВТИмяРегистраСрезПоследних"".
	|Позволяет получить срез последних регистра.
	|Учитываются такие особенности ЗУП как возвратные события, несколько событий на дату и т.п. 
	|Для повышения производительности используются регистры с постфиксом Интервальный (при их наличии).";
	
	Возврат Справка;
КонецФункции


Функция ИмяПредставления(ИмяРегистра) Экспорт
	Возврат "ИсполняемоеПредставление.РегистрСведений." + ИмяРегистра + ".СрезПоследних";
КонецФункции

// Исполнить.
// 
// Параметры:
//  ПараметрыВыполнения - см. ЭлементыМоделиИсполненияПредставлений.НовыйПараметрыВыполненияПредставления
//  Запрос - Запрос
// 
// Возвращаемое значение:
// 	- РезультатЗапроса 
Функция Исполнить(ПараметрыВыполнения, Запрос) Экспорт
	ЧастиИмени = СтрРазделить(ПараметрыВыполнения.ИмяИсполняемогоПредставления, ".");
	ИмяРегистра = ЧастиИмени[ЧастиИмени.ВГраница() - 1];
	
	ВТФильтр = ПараметрыВыполнения.ОписаниеВТФильтр;
	Измерения = Новый Массив();
	Для Каждого КлючЗначение Из ВТФильтр.ПсевдонимыПолей Цикл
		Если ВРег(КлючЗначение.Ключ) <> "ПЕРИОД" Тогда
			Измерения.Добавить(КлючЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
		
	ОписаниеВТФильтр = ЗарплатаКадрыПериодическиеРегистры.ОписаниеФильтраДляСоздатьВТИмяРегистраПоВременнойТаблице(
		ВТФильтр.ИмяВТ,
		Измерения	
	);	
	ОписаниеВТФильтр.СоответствиеИзмеренийРегистраИзмерениямФильтра = ВТФильтр.ПсевдонимыПолей;
	
	ПараметрыПостроения = ЗарплатаКадрыПериодическиеРегистры.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ПараметрыПостроения.ИндексироватьПо = ПараметрыВыполнения.Индексы;
	
	ПараметрФормироватьСПериодичносьюДень = ПараметрыВыполнения.ЗначенияПараметровКонстант.Получить("ФормироватьСПериодичностьДень");
	Если ПараметрФормироватьСПериодичносьюДень <> Неопределено Тогда
		ПараметрыПостроения.ФормироватьСПериодичностьДень = ПараметрФормироватьСПериодичносьюДень.Значение;
	КонецЕсли;
	
	ПараметрВсеЗаписи = ПараметрыВыполнения.ЗначенияПараметровКонстант.Получить("ВсеЗаписи");
	Если ПараметрВсеЗаписи <> Неопределено Тогда
		ПараметрыПостроения.ВсеЗаписи = ПараметрВсеЗаписи.Значение;
	КонецЕсли;
	
	ПараметрВключаяГраницу = ПараметрыВыполнения.ЗначенияПараметровКонстант.Получить("ВключаяГраницу");
	Если ПараметрВсеЗаписи <> Неопределено Тогда
		ПараметрыПостроения.ВключаяГраницу = ПараметрВключаяГраницу.Значение;
	КонецЕсли;
	
	ИсполнительПредставленийЗУПУтилиты.ЗаполнитьКоллекциюОтборов(
		ПараметрыПостроения.ОтборыПрименяемыеКСрезу, 
		ПараметрыВыполнения.Отборы, 
		Запрос.Параметры);
	
	ПараметрыПостроения.СоответствиеПсевдонимовПолей = ПараметрыВыполнения.ПсевдонимыПолей;
	ПараметрыПостроения.ИндексироватьПо = ПараметрыВыполнения.Индексы;
	
	Если ЗначениеЗаполнено(ПараметрыВыполнения.ИмяВТРезультат) Тогда
		ЗарплатаКадрыПериодическиеРегистры.СоздатьВТИмяРегистраСрезПоследних(
			ИмяРегистра, 
			Запрос.МенеджерВременныхТаблиц,
			ПараметрыВыполнения.ТолькоРазрешенные, 
			ОписаниеВТФильтр,
			ПараметрыПостроения,
			ПараметрыВыполнения.ИмяВТРезультат);
		
		Возврат Неопределено;
	Иначе
		ЗапросСреза = ЗарплатаКадрыПериодическиеРегистры.ЗапросВТИмяРегистраСрез(
			ИмяРегистра,
			ПараметрыВыполнения.ТолькоРазрешенные, 
			ОписаниеВТФильтр, 
			ПараметрыПостроения, 
			Истина,
			"");
		
		ЗапросСреза.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
		Возврат ЗапросСреза.Выполнить();	
	КонецЕсли;
КонецФункции

Функция ИсполняемыйКод(ПараметрыВыполнения, Знач ТекущиеТабуляции) Экспорт
	ЧастиИмени = СтрРазделить(ПараметрыВыполнения.ИмяИсполняемогоПредставления, ".");
	ИмяРегистра = ЧастиИмени[ЧастиИмени.ВГраница() - 1];
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	
	ВТФильтр = ПараметрыВыполнения.ОписаниеВТФильтр;
	
	Строка = "Измерения = Новый Массив();";
	ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Строка);
	Строка = "СоответствиеИзмерений = Новый Соответствие();";
	ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Строка);
	
	Для Каждого КлючЗначение Из ВТФильтр.ПсевдонимыПолей Цикл
		Если ВРег(КлючЗначение.Ключ) <> "ПЕРИОД" Тогда
			Строка = "Измерения.Добавить(" + """" + КлючЗначение.Ключ + """);";
			ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Строка);
		КонецЕсли;
		Строка = "СоответствиеИзмерений.Вставить(" + """" + КлючЗначение.Ключ + """, " + """" + КлючЗначение.Значение + """);";
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Строка);
	КонецЦикла;
	ТекстовыйДокумент.ДобавитьСтроку("");
	
	Строка = "ОписаниеВТФильтр = ЗарплатаКадрыПериодическиеРегистры.ОписаниеФильтраДляСоздатьВТИмяРегистраПоВременнойТаблице(";
	ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Строка);
	ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Символы.Таб + """" + ВТФильтр.ИмяВТ + """,");
	ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Символы.Таб + "Измерения);");
	
	Строка = "ОписаниеВТФильтр.СоответствиеИзмеренийРегистраИзмерениямФильтра = СоответствиеИзмерений;";
	ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Строка);
	
	Строка = "ПараметрыПостроения = ЗарплатаКадрыПериодическиеРегистры.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();";
	ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Строка);
	
	ТекстовыйДокумент.ДобавитьСтроку("");
	Строка = "ПараметрыПостроения.ИндексироватьПо = Новый Массив;";
	ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Строка);
	ГенерацияИсполняемогоКодаПредставленийУтилиты.КодЗаполненияКоллекцииИндексовВТекст(
		ТекстовыйДокумент, 
		"ПараметрыПостроения.ИндексироватьПо", 
		ПараметрыВыполнения, 
		ТекущиеТабуляции);	
	
	ТекстовыйДокумент.ДобавитьСтроку("");
	ГенерацияИсполняемогоКодаПредставленийУтилиты.КодЗаполненияПсевдонимовПолейВТекст(
		ТекстовыйДокумент, 
		"ПараметрыПостроения.СоответствиеПсевдонимовПолей", 
		ПараметрыВыполнения, 
		ТекущиеТабуляции);	
		
	ТекстовыйДокумент.ДобавитьСтроку("");
	
	Строка = ГенерацияИсполняемогоКодаПредставленийУтилиты.СтрокаПрисвоенияПараметровПеременной(
		"ПараметрыПостроения.ФормироватьСПериодичностьДень", 
		"ФормироватьСПериодичностьДень",
		ПараметрыВыполнения,
		ТекущиеТабуляции);	
	ТекстовыйДокумент.ДобавитьСтроку(Строка);
	
	Строка = ГенерацияИсполняемогоКодаПредставленийУтилиты.СтрокаПрисвоенияПараметровПеременной(
		"ПараметрыПостроения.ВсеЗаписи", 
		"ВсеЗаписи",
		ПараметрыВыполнения,
		ТекущиеТабуляции);	
	ТекстовыйДокумент.ДобавитьСтроку(Строка);
	
	Строка = ГенерацияИсполняемогоКодаПредставленийУтилиты.СтрокаПрисвоенияПараметровПеременной(
		"ПараметрыПостроения.ВключаяГраницу", 
		"ВключаяГраницу",
		ПараметрыВыполнения,
		ТекущиеТабуляции);	
	ТекстовыйДокумент.ДобавитьСтроку(Строка);
	
	ГенерацияИсполняемогоКодаПредставленийЗУПУтилиты.КодЗаполненияКоллекцииОтборовВМассивСтрок(
		ТекстовыйДокумент, 
		"ПараметрыПостроения.ОтборыПрименяемыеКСрезу", 
		ПараметрыВыполнения.Отборы, 
		ТекущиеТабуляции);
	
	
	Если ЗначениеЗаполнено(ПараметрыВыполнения.ИмяВТРезультат) Тогда
		Строка = "ЗарплатаКадрыПериодическиеРегистры.СоздатьВТИмяРегистраСрезПоследних(";
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Строка);
		Строка = """" + ИмяРегистра + """,";
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Символы.Таб + Строка);
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Символы.Таб + "Запрос.МенеджерВременныхТаблиц,");
		Строка = ГенерацияИсполняемогоКодаПредставленийУтилиты.ПримитивноеЗначениеВСтроку(ПараметрыВыполнения.ТолькоРазрешенные) + ",";
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Символы.Таб + Строка);
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Символы.Таб + "ОписаниеВТФильтр,");
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Символы.Таб + "ПараметрыПостроения,");
		Строка = ГенерацияИсполняемогоКодаПредставленийУтилиты.ПримитивноеЗначениеВСтроку(ПараметрыВыполнения.ИмяВТРезультат) + ");";
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Символы.Таб + Строка);
	Иначе
		Строка = "ЗапросСреза = ЗарплатаКадрыПериодическиеРегистры.ЗапросВТИмяРегистраСрез(";
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Строка);
		Строка = """" + ИмяРегистра + """,";
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Строка);
		Строка = ГенерацияИсполняемогоКодаПредставленийУтилиты.ПримитивноеЗначениеВСтроку(ПараметрыВыполнения.ТолькоРазрешенные) + ",";
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Символы.Таб + Строка);
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Символы.Таб + "ОписаниеВТФильтр,");
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Символы.Таб + "ПараметрыПостроения,");
		Строка = ГенерацияИсполняемогоКодаПредставленийУтилиты.ПримитивноеЗначениеВСтроку(Истина) + ",";
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Символы.Таб + Строка);
		Строка = ГенерацияИсполняемогоКодаПредставленийУтилиты.ПримитивноеЗначениеВСтроку("") + ");";
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Символы.Таб + Строка);
		Строка = "ЗапросСреза.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;";
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + Строка);
		
		ТекстовыйДокумент.ДобавитьСтроку(ТекущиеТабуляции + "Возврат ЗапросСреза.Выполнить();");		
	КонецЕсли;
	
	Возврат ТекстовыйДокумент.ПолучитьТекст();
КонецФункции

// Текст запроса для СКД.
// 
// Параметры:
//  ПараметрыВыполнения - см. ЭлементыМоделиИсполненияПредставлений.НовыйПараметрыВыполненияПредставления
// 
// Возвращаемое значение:
//  - Строка
Функция ТекстЗапросаДляСКД(ПараметрыВыполнения) Экспорт
	Описание = ПоставщикИсполняемыхПредставлений.ОписаниеПредставленияПоИмени(ПараметрыВыполнения.ИмяИсполняемогоПредставления);
	
	Модель = ГенерацияИсполняемогоКодаПредставленийУтилиты.МодельЗапросаДляСКД(ПараметрыВыполнения, Описание);
	Построитель = МодельЗапросаУтилиты.СоздатьПостроительМодели(Модель);
	ПараметрФормироватьСПериодичносьюДень = ПараметрыВыполнения.ЗначенияПараметровКонстант.Получить("ФормироватьСПериодичностьДень");
	Если ПараметрФормироватьСПериодичносьюДень <> Неопределено Тогда
		ВыражениеПараметра = 
			ГенерацияИсполняемогоКодаПредставленийУтилиты.ПримитивноеЗначениеВСтроку("ПараметрыПостроения_ФормироватьСПериодичностьДень") 
			+ " = "
			+ ГенерацияИсполняемогоКодаПредставленийУтилиты.ПримитивноеЗначениеВСтроку(ПараметрФормироватьСПериодичносьюДень.Значение);
		
		Построитель.ДобавитьОтбор(ВыражениеПараметра);
	КонецЕсли; 
	
	ПараметрВсеЗаписи = ПараметрыВыполнения.ЗначенияПараметровКонстант.Получить("ВсеЗаписи");
	Если ПараметрВсеЗаписи <> Неопределено Тогда
		ВыражениеПараметра = 
			ГенерацияИсполняемогоКодаПредставленийУтилиты.ПримитивноеЗначениеВСтроку("ПараметрыПостроения_ВсеЗаписи") 
			+ " = "
			+ ГенерацияИсполняемогоКодаПредставленийУтилиты.ПримитивноеЗначениеВСтроку(ПараметрВсеЗаписи.Значение);
		
		Построитель.ДобавитьОтбор(ВыражениеПараметра);
	КонецЕсли; 
	
	ПараметрВключаяГраницу = ПараметрыВыполнения.ЗначенияПараметровКонстант.Получить("ВключаяГраницу");
	Если ПараметрВключаяГраницу <> Неопределено Тогда
		ВыражениеПараметра = 
			ГенерацияИсполняемогоКодаПредставленийУтилиты.ПримитивноеЗначениеВСтроку("ПараметрыПостроения_ВключаяГраницу") 
			+ " = "
			+ ГенерацияИсполняемогоКодаПредставленийУтилиты.ПримитивноеЗначениеВСтроку(ПараметрВключаяГраницу.Значение);
		
		Построитель.ДобавитьОтбор(ВыражениеПараметра);
	КонецЕсли; 
	
	Запрос = Построитель.ПолучитьМодель().Элементы[0];
	ТекстЗапроса = ГенерацияТекстовЗапросов.ТекстЗапросаВыбора(Запрос);
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#КонецЕсли
