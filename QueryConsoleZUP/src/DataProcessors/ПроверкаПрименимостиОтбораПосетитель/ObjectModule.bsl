#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПеременныеМодуля

Перем СтекРезультатов;         // Массив - Используется как стек для хранения результатов анализа подузлов.
Перем ОписаниеОтбора;          // см. ЭлементыМоделиОписанияПредставлений.НовыйОписаниеОтбора
Перем ИменаДоступныхПолей;     // Соответствие - Для быстрого поиска доступных для отбора полей.
Перем КартаРезультатовАнализа; // Соответствие - Ключ: Узел дерева, Значение: Результат анализа для этого узла.

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// В этой области должны быть публичные функции, такие как ВыделитьИМодифицироватьОтбор.
// Я перенес их в конец модуля в отдельную область для служебных процедур.

#КонецОбласти

#Область МетодыПосетителя

// Этот метод является частью "контракта" для обработки вложенных запросов.
// Он должен быть реализован в тех посетителях, которые могут встретить вложенный запрос.
//
// Параметры:
//  УзелЗапроса - см. ЭлементыМоделиЗапроса.НовыйЗапросВыбора
Процедура ПосетитьВложенныйЗапрос(УзелЗапроса) Экспорт
	ЭтоДопустимыйЭлементОтбора = ОписаниеОтбора.ПоддерживаемыеОперации.ВложенныйЗапрос;
	ПоместитьРезультат(УзелЗапроса, ЭтоДопустимыйЭлементОтбора, "вложенный запрос");
КонецПроцедуры

// Бинарная операция при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйБинарнаяОперация
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура БинарнаяОперацияПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Бинарная операция при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйБинарнаяОперация
Процедура БинарнаяОперацияПриВыходе(Выражение) Экспорт
	РезультатПравойЧасти = ИзвлечьРезультатИзСтека();
	РезультатЛевойЧасти = ИзвлечьРезультатИзСтека();
	
	МожноДелегировать = РезультатЛевойЧасти.МожноДелегировать И РезультатПравойЧасти.МожноДелегировать;
	
	Если МожноДелегировать Тогда
		Операция = Выражение.Операция;
		Если Операция = "И" Тогда
			МожноДелегировать = Истина; // "И" поддерживается всегда, если его части делегируемы.
		ИначеЕсли Операция = "ИЛИ" Тогда
			МожноДелегировать = ОписаниеОтбора.ПоддерживаемыеОперации.ИЛИ;
		ИначеЕсли Операция = "=" Или Операция = "<>" Или Операция = ">=" Или Операция = "<=" 
			Или Операция = "<" Или Операция = "<" Или Операция = ">" Тогда
			
			МожноДелегировать = ОписаниеОтбора.ПоддерживаемыеОперации.Сравнение;
		ИначеЕсли Операция = "+" Или Операция = "-" Или Операция = "*" Или Операция = "\" Тогда
			МожноДелегировать = ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;
		Иначе
			МожноДелегировать = Ложь; // Неизвестная бинарная операция.
		КонецЕсли;
	КонецЕсли;
	
	ПоместитьРезультат(Выражение, МожноДелегировать, Выражение.Операция);
КонецПроцедуры

// Оператор между при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОператорМежду
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ОператорМеждуПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Оператор между при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОператорМежду
Процедура ОператорМеждуПриВыходе(Выражение) Экспорт
	МожноДелегироватьКонец = ИзвлечьРезультатИзСтека();
	МожноДелегироватьНачало = ИзвлечьРезультатИзСтека();
	МожноДелегироватьОперанд = ИзвлечьРезультатИзСтека();
	
	МожноДелегировать = ОписаниеОтбора.ПоддерживаемыеОперации.МЕЖДУ
		И МожноДелегироватьКонец
		И МожноДелегироватьНачало
		И МожноДелегироватьОперанд;
		
	ПоместитьРезультат(Выражение.Операция, МожноДелегировать, "МЕЖДУ");
КонецПроцедуры

// Оператор проверки типа при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОператорПроверкиТипа
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ОператорПроверкиТипаПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Оператор проверки типа при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОператорПроверкиТипа
Процедура ОператорПроверкиТипаПриВыходе(Выражение) Экспорт
	//
КонецПроцедуры

// Оператор проверки на NULL при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОператорПроверкиНаNULL
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ОператорПроверкиНаNULLПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Оператор проверки на NULL при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОператорПроверкиНаNULL
Процедура ОператорПроверкиНаNULLПриВыходе(Выражение) Экспорт
	МожноДелегироватьОперанд = ИзвлечьРезультатИзСтека();
	
	// Оператор IS NULL/IS NOT NULL является частным случаем сравнения.
	МожноДелегировать = ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений
		И МожноДелегироватьОперанд;
		
	ПоместитьРезультат(Выражение, МожноДелегировать, "ЕСТЬ NULL");
КонецПроцедуры

// Список выражений при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйСписокВыражений
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура СписокВыраженийПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Список выражений при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйСписокВыражений
Процедура СписокВыраженийПриВыходе(Выражение) Экспорт
	МожноДелегировать = Истина;
	
	Для Индекс = 0 По Выражение.Элементы.ВГраница() Цикл
		МожноДелегировать = МожноДелегировать И ИзвлечьРезультатИзСтека();
	КонецЦикла;

	Если Выражение.Элементы.Количество() > 1 Тогда
		МожноДелегировать = МожноДелегировать И ОписаниеОтбора.ПоддерживаемыеОперации.СписокПолей;
	КонецЕсли;	
	
	ПоместитьРезультат(Выражение, МожноДелегировать, "спсок выражений");
КонецПроцедуры

// Оператор В при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОператорВ
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ОператорВПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Оператор В при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОператорВ
Процедура ОператорВПриВыходе(Выражение) Экспорт
	МожноДелегироватьСписок = ИзвлечьРезультатИзСтека();
	МожноДелегироватьОперанд = ИзвлечьРезультатИзСтека();
	
	// Для "В ИЕРАРХИИ" и обычного "В" могут быть разные флаги поддержки.
	Если Выражение.Иерархия Тогда
		МожноДелегировать = ОписаниеОтбора.ПоддерживаемыеОперации.ВИерархии;
		Операция = "В иеерархии";
	Иначе
		МожноДелегировать = ОписаниеОтбора.ПоддерживаемыеОперации.ВСписок;
		Операция = "В";
	КонецЕсли;
	
	МожноДелегировать = МожноДелегировать
		И МожноДелегироватьСписок
		И МожноДелегироватьОперанд;
		
	ПоместитьРезультат(Выражение, МожноДелегировать, Операция);
КонецПроцедуры

// Логическое отрицание при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйЛогическоеОтрицание
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ЛогическоеОтрицаниеПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Логическое отрицание при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйЛогическоеОтрицание
Процедура ЛогическоеОтрицаниеПриВыходе(Выражение) Экспорт
	// Условие с "НЕ" делегируемо, если вложенное условие делегируемо.
	МожноДелегироватьВыражение = ИзвлечьРезультатИзСтека();
	МожноДелегировать = МожноДелегироватьВыражение И ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;
	ПоместитьРезультат(Выражение, МожноДелегировать, "НЕ");
КонецПроцедуры

// Унарная операция при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйУнарнаяОперация
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура УнарнаяОперацияПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Унарная операция при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйУнарнаяОперация
Процедура УнарнаяОперацияПриВыходе(Выражение) Экспорт
	МожноДелегироватьОперанд = ИзвлечьРезультатИзСтека();
	
	МожноДелегировать = ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений 
		И МожноДелегироватьОперанд;
	
	ПоместитьРезультат(Выражение, МожноДелегировать, "-");
КонецПроцедуры

// Оператор подобно при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОператорПодобно
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ОператорПодобноПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Оператор подобно при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОператорПодобно
Процедура ОператорПодобноПриВыходе(Выражение) Экспорт
	МожноДелегироватьШаблон = ИзвлечьРезультатИзСтека();
	МожноДелегироватьОперанд = ИзвлечьРезультатИзСтека();
	
	МожноДелегировать = ОписаниеОтбора.ПоддерживаемыеОперации.ПОДОБНО
		И МожноДелегироватьШаблон
		И МожноДелегироватьОперанд;

	ПоместитьРезультат(Выражение, МожноДелегировать, "ПОДОБНО");
КонецПроцедуры

// Разыменование при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйРазыменование
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура РазыменованиеПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Ложь;
КонецПроцедуры

// Разыменование при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйРазыменование
Процедура РазыменованиеПриВыходе(Выражение) Экспорт
	МожноДелегировать = Истина;
	Для Каждого ЭлементРазыменования Из Выражение.Элементы Цикл
		Если ЭлементРазыменования.Тип = "ПолеИсточника" Тогда
			// проверить что это тот источник отборы для когорого мы обрабаьываем и что поле в доступных для отбора полях
			МожноДелегировать = Истина;
		ИначеЕсли ТипЗнч(ЭлементРазыменования) = Тип("Строка") Тогда 
			// Это значит что у нас выражение типа "ДанныеСотрудников.Сотрудник.Код"
			МожноДелегировать = МожноДелегировать И ОписаниеОтбора.ПоддерживаемыеОперации.РазыменованиеПолей;
		Иначе
			ОбходМоделиЯзыкаВыражений.ОбойтиДерево(ЭлементРазыменования, ЭтотОбъект);
			МожноДелегировать = МожноДелегировать И ИзвлечьРезультатИзСтека();
		КонецЕсли;	
	КонецЦикла;
	
	ПоместитьРезультат(Выражение, МожноДелегировать, "разыменование");
КонецПроцедуры

// Приведение типа при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйПриведениеТипа
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ПриведениеТипаПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Приведение типа при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйПриведениеТипа
Процедура ПриведениеТипаПриВыходе(Выражение) Экспорт
	//
КонецПроцедуры

// Тип ссылочного поля при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйТипСсылочногоПоля
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ТипСсылочногоПоляПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Тип ссылочного поля при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйТипСсылочногоПоля
Процедура ТипСсылочногоПоляПриВыходе(Выражение) Экспорт
	//
КонецПроцедуры

// Описание типа булево при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОписаниеТипаБулево
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ОписаниеТипаБулевоПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Описание типа булево при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОписаниеТипаБулево
Процедура ОписаниеТипаБулевоПриВыходе(Выражение) Экспорт
	//
КонецПроцедуры

// Описание типа дата при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОписаниеТипаДата
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ОписаниеТипаДатаПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Описание типа дата при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОписаниеТипаДата
Процедура ОписаниеТипаДатаПриВыходе(Выражение) Экспорт
	//
КонецПроцедуры

// Описание типа число при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОписаниеТипаЧисло
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ОписаниеТипаЧислоПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Описание типа число при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОписаниеТипаЧисло
Процедура ОписаниеТипаЧислоПриВыходе(Выражение) Экспорт
	//
КонецПроцедуры

// Описание типа строка при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОписаниеТипаСтрока
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ОписаниеТипаСтрокаПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Описание типа строка при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйОписаниеТипаСтрока
Процедура ОписаниеТипаСтрокаПриВыходе(Выражение) Экспорт
	//
КонецПроцедуры

// Выбор при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйВыбор
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ВыборПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Выбор при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйВыбор
Процедура ВыборПриВыходе(Выражение) Экспорт
	МожноДелегировать = ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;
	
	Если Выражение.Иначе <> Неопределено Тогда
		МожноДелегировать = ИзвлечьРезультатИзСтека()
	КонецЕсли;
	
	Для Индекс = 0 По Выражение.АльтернативыВыбора.ВГраница() Цикл
		МожноДелегироватьДействие = ИзвлечьРезультатИзСтека();
		МожноДелегироватьУсловие = ИзвлечьРезультатИзСтека();
		МожноДелегировать = МожноДелегировать И МожноДелегироватьДействие И МожноДелегироватьУсловие;
	КонецЦикла;//	
	
	ПоместитьРезультат(Выражение, МожноДелегировать, "ВЫБОР");
КонецПроцедуры

// Константа при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйКонстанта
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура КонстантаПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Константа при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйКонстанта
Процедура КонстантаПриВыходе(Выражение) Экспорт
	МожноДелегировать = ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;
	ПоместитьРезультат(Выражение, Истина, "Константа");
КонецПроцедуры

// Параметр запроса при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйПараметрЗапроса
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ПараметрЗапросаПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Параметр запроса при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйПараметрЗапроса
Процедура ПараметрЗапросаПриВыходе(Выражение) Экспорт
	ПоместитьРезультат(Выражение, Истина, "параметр запроса");
КонецПроцедуры

// Агрегатная функция при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйАгрегатнаяФункция
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура АгрегатнаяФункцияПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Агрегатная функция при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйАгрегатнаяФункция
Процедура АгрегатнаяФункцияПриВыходе(Выражение) Экспорт
	ИзвлечьРезультатИзСтека();
	
	// Агрегатные функции нельзя использовать в отборе исполняемых представлений	
	ПоместитьРезультат(Выражение, Ложь, "агрегатная функция");
КонецПроцедуры

// Агрегатная функция количество при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйАгрегатнаяФункцияКоличество
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура АгрегатнаяФункцияКоличествоПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Агрегатная функция количество при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйАгрегатнаяФункцияКоличество
Процедура АгрегатнаяФункцияКоличествоПриВыходе(Выражение) Экспорт
	ИзвлечьРезультатИзСтека();
	
	// Агрегатные функции нельзя использовать в отборе исполняемых представлений	
	ПоместитьРезультат(Выражение, Ложь, "агрегатная функция");
КонецПроцедуры

// Функция часть периода числом при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияЧастьПериодаЧислом
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ФункцияЧастьПериодаЧисломПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Функция часть периода числом при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияЧастьПериодаЧислом
Процедура ФункцияЧастьПериодаЧисломПриВыходе(Выражение) Экспорт
	МожноДелегироватьАргумент = ИзвлечьРезультатИзСтека();
	
	МожноДелегировать = МожноДелегироватьАргумент И ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;	
	ПоместитьРезультат(Выражение, Ложь, "агрегатная функция");
КонецПроцедуры

// Функция начало периода при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияНачалоПериода
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ФункцияНачалоПериодаПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Функция начало периода при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияНачалоПериода
Процедура ФункцияНачалоПериодаПриВыходе(Выражение) Экспорт
	МожноДелегироватьАргумент = ИзвлечьРезультатИзСтека();
	
	МожноДелегировать = МожноДелегироватьАргумент И ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;	
	ПоместитьРезультат(Выражение, МожноДелегировать, "ФункцияНачалоПериода");
КонецПроцедуры

// Функция конец периода при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияКонецПериода
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ФункцияКонецПериодаПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Функция конец периода при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияКонецПериода
Процедура ФункцияКонецПериодаПриВыходе(Выражение) Экспорт
	МожноДелегироватьАргумент = ИзвлечьРезультатИзСтека();
	
	МожноДелегировать = МожноДелегироватьАргумент И ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;	
	ПоместитьРезультат(Выражение, МожноДелегировать, "ФункцияКонецПериода");
КонецПроцедуры

// Функция добавить к дате при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияДобавитьКДате
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ФункцияДобавитьКДатеПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Функция добавить к дате при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияДобавитьКДате
Процедура ФункцияДобавитьКДатеПриВыходе(Выражение) Экспорт
	МожноДелегироватьСдвиг = ИзвлечьРезультатИзСтека();
	МожноДелегироватьДата = ИзвлечьРезультатИзСтека();
	
	МожноДелегировать = МожноДелегироватьДата И МожноДелегироватьСдвиг И ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;	
	ПоместитьРезультат(Выражение, МожноДелегировать, "ФункцияДобавитьКДате");
КонецПроцедуры

// Функция дата время при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияДатаВремя
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ФункцияДатаВремяПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Функция дата время при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияДатаВремя
Процедура ФункцияДатаВремяПриВыходе(Выражение) Экспорт
	МожноДелегировать = ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;
	ПоместитьРезультат(Выражение, МожноДелегировать, "ФункцияДатаВремя");
КонецПроцедуры

// Функция IsNull при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияIsNull
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ФункцияIsNullПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Функция IsNull при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияIsNull
Процедура ФункцияIsNullПриВыходе(Выражение) Экспорт
	МожноДелегироватьАргумент2 = ИзвлечьРезультатИзСтека();
	МожноДелегироватьАргумент1 = ИзвлечьРезультатИзСтека();
		
	МожноДелегировать = МожноДелегироватьАргумент1 И МожноДелегироватьАргумент2 И ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;	
	ПоместитьРезультат(Выражение, МожноДелегировать, "ФункцияIsNull");
КонецПроцедуры

// Функция представление при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияПредставление
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ФункцияПредставлениеПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Функция представление при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияПредставление
Процедура ФункцияПредставлениеПриВыходе(Выражение) Экспорт
	МожноДелегировать = ИзвлечьРезультатИзСтека();
	
	МожноДелегировать = МожноДелегировать И ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;	
	ПоместитьРезультат(Выражение, МожноДелегировать, "ФункцияПредставление");
КонецПроцедуры

// Функция представление ссылки при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияПредставлениеСсылки
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ФункцияПредставлениеСсылкиПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Функция представление ссылки при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияПредставлениеСсылки
Процедура ФункцияПредставлениеСсылкиПриВыходе(Выражение) Экспорт
	МожноДелегировать = ИзвлечьРезультатИзСтека();
	
	МожноДелегировать = МожноДелегировать И ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;	
	ПоместитьРезультат(Выражение, МожноДелегировать, "ФункцияПредставлениеСсылки");
КонецПроцедуры

// Функция тип значения при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияТипЗначения
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ФункцияТипЗначенияПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Функция тип значения при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияТипЗначения
Процедура ФункцияТипЗначенияПриВыходе(Выражение) Экспорт	
	МожноДелегировать = ИзвлечьРезультатИзСтека();
	
	МожноДелегировать = МожноДелегировать И ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;	
	ПоместитьРезультат(Выражение, МожноДелегировать, "ФункцияТипЗначения");
КонецПроцедуры

// Функция значение при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияЗначение
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ФункцияЗначениеПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Функция значение при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияЗначение
Процедура ФункцияЗначениеПриВыходе(Выражение) Экспорт
	ПоместитьРезультат(Выражение, ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений, "ФункцияЗначение");
КонецПроцедуры

// Функция тип при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияТип
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ФункцияТипПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;	
КонецПроцедуры

// Функция тип при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияТип
Процедура ФункцияТипПриВыходе(Выражение) Экспорт
	МожноДелегировать = ИзвлечьРезультатИзСтека();
	
	МожноДелегировать = МожноДелегировать И ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;	
	ПоместитьРезультат(Выражение, ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений, " ФункцияТип");
КонецПроцедуры

// Функция разность дат при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияРазностьДат
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ФункцияРазностьДатПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Функция разность дат при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйФункцияРазностьДат
Процедура ФункцияРазностьДатПриВыходе(Выражение) Экспорт
	МожноДелегироватьАргумент2 = ИзвлечьРезультатИзСтека();
	МожноДелегироватьАргумент1 = ИзвлечьРезультатИзСтека();
		
	МожноДелегировать = МожноДелегироватьАргумент1 И МожноДелегироватьАргумент2 И ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений;	
	ПоместитьРезультат(Выражение, ОписаниеОтбора.ПоддерживаемыеОперации.ЯзыкВыражений, " ФункцияРазностьДат");
КонецПроцедуры

// Выражение все поля при входе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйВыражениеВсеПоля
//  ВыполнятьСтандартныйОбход - Булево - Управляет стандартным обходом дочерних узлов.
//      Если установить в Ложь, обходчик не будет рекурсивно посещать дочерние узлы.
Процедура ВыражениеВсеПоляПриВходе(Выражение, ВыполнятьСтандартныйОбход) Экспорт
	ВыполнятьСтандартныйОбход = Истина;
КонецПроцедуры

// Выражение все поля при выходе.
//
// Параметры:
//  Выражение - см. ЭлементыМоделиЗапроса.НовыйВыражениеВсеПоля
Процедура ВыражениеВсеПоляПриВыходе(Выражение) Экспорт
	ПоместитьРезультат(Выражение, Ложь, " ВыражениеВсе");
КонецПроцедуры

#КонецОбласти

Процедура ПоместитьРезультат(Выражение, ЭтоДопустимыйЭлементОтбора, Операция)
	СтекРезультатов.Добавить(ЭтоДопустимыйЭлементОтбора);
	
	КартаРезультатовАнализа.Вставить(Выражение, ЭтоДопустимыйЭлементОтбора);
КонецПроцедуры

Функция ИзвлечьРезультатИзСтека()
	Элемент = СтекРезультатов[СтекРезультатов.ВГраница()];
	СтекРезультатов.Удалить(СтекРезультатов.ВГраница());
	
	Возврат Элемент;
КонецФункции

Функция ВыделитьИМодифицироватьОтбор(Условие, КартаРезультатов) Экспорт
	
	Результат = Новый Структура("НовоеУсловие, ДелегируемыеУсловия");
	Результат.ДелегируемыеУсловия = Новый Массив;
	
	// Запускаем рекурсивный процесс, который вернет новое дерево
	// и одновременно наполнит массив ДелегируемыеУсловия.
	Результат.НовоеУсловие = МодифицироватьИСобрать(Условие, КартаРезультатов, Результат.ДелегируемыеУсловия);
	
	// Финальная очистка: если в итоге осталось только "ИСТИНА", значит, вся секция ГДЕ не нужна.
	Если Результат.НовоеУсловие <> Неопределено 
		И Результат.НовоеУсловие.Тип = "Константа" 
		И Результат.НовоеУсловие.Значение = Истина Тогда
		
		Результат.НовоеУсловие = Неопределено;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция МодифицироватьИСобрать(Выражение, КартаРезультатов, СписокДелегируемыхУсловий)
	ЭтоОператорИ = (Выражение.Тип = "БинарнаяОперация" И Выражение.Операция = "И");
	
	Если ЭтоОператорИ Тогда
		НоваяЛеваяЧасть = МодифицироватьИСобрать(Выражение.ЛеваяЧасть, КартаРезультатов, СписокДелегируемыхУсловий);
		НоваяПраваяЧасть = МодифицироватьИСобрать(Выражение.ПраваяЧасть, КартаРезультатов, СписокДелегируемыхУсловий);
		
		// "Схлопываем" дерево, если одна из веток превратилась в ИСТИНА.
		// (ИСТИНА И X) = X
		// (X И ИСТИНА) = X
		ЛеваяЧастьИстина = (НоваяЛеваяЧасть.Тип = "Константа" И НоваяЛеваяЧасть.Значение = Истина);
		ПраваяЧастьИстина = (НоваяПраваяЧасть.Тип = "Константа" И НоваяПраваяЧасть.Значение = Истина);
		
		Если ЛеваяЧастьИстина И ПраваяЧастьИстина Тогда
			Возврат ЭлементыМоделиЗапроса.НовыйКонстанта(Истина);
		ИначеЕсли ЛеваяЧастьИстина Тогда
			Возврат НоваяПраваяЧасть;
		ИначеЕсли ПраваяЧастьИстина Тогда
			Возврат НоваяЛеваяЧасть;
		Иначе
			НовыйУзел = ЭлементыМоделиЗапроса.НовыйБинарнаяОперация();
			НовыйУзел.Операция = "И";
			НовыйУзел.ЛеваяЧасть = НоваяЛеваяЧасть;
			НовыйУзел.ПраваяЧасть = НоваяПраваяЧасть;
			Возврат НовыйУзел;
		КонецЕсли;
	Иначе
		РезультатАнализа = КартаРезультатов.Получить(Выражение);
		Если РезультатАнализа = Истина Тогда
			СписокДелегируемыхУсловий.Добавить(МодельЗапросаУтилиты.СкопироватьЭлементМоделиЗапроса(Выражение));
			ВыражениеКонстанта = ЭлементыМоделиЗапроса.НовыйКонстанта();
			ВыражениеКонстанта.Значение = Истина;
			
			Возврат ВыражениеКонстанта;
		Иначе
			Возврат Выражение;
		КонецЕсли;
	КонецЕсли;
КонецФункции

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли