
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем ИсполняемыйЗапрос;
	
#Область СлужебныеПроцедурыИФункции

Процедура Инициализировать(ЗначенияПараметровЗапроса, МенеджерВременныхТаблиц = Неопределено) Экспорт
	Если МенеджерВременныхТаблиц = Неопределено Тогда
		ИсполняемыйЗапрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Иначе
		ИсполняемыйЗапрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	КонецЕсли;
		
	Для Каждого КлючЗначение Из ЗначенияПараметровЗапроса Цикл
		ИсполняемыйЗапрос.УстановитьПараметр(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;	
КонецПроцедуры

Функция ИсполнитьУзелИсполняемоеПредставление(ИсполняемоеПредставление, ПараметрыИсполнения) Экспорт
	
	Исполнитель = ПоставщикИсполняемыхПредставлений.ИсполнительПредставления(ИсполняемоеПредставление.ИмяТаблицы);
	
	Возврат Исполнитель.Исполнить(ПараметрыИсполнения, ИсполняемыйЗапрос);		
КонецФункции

Функция ИсполнитьУзелЗапросУничтожения(ЗапросУничтожения) Экспорт
	ТекстЗапроса = ГенерацияТекстовЗапросов.ТекстЗапросаУничтожения(ЗапросУничтожения);
	ИсполняемыйЗапрос.Текст = ТекстЗапроса;	
	Возврат ИсполняемыйЗапрос.Выполнить();
КонецФункции

Функция ИсполнитьУзелЗапросВыбора(ЗапросВыбора) Экспорт
	ТекстЗапроса = ГенерацияТекстовЗапросов.ТекстЗапросаВыбора(ЗапросВыбора);
	ИсполняемыйЗапрос.Текст = ТекстЗапроса;
	
	Возврат ИсполняемыйЗапрос.Выполнить();
КонецФункции

Функция ИсполнитьУзелУничтожитьВТ(ИмяВТ) Экспорт
	ИсполняемыйЗапрос.Текст = "УНИЧТОЖИТЬ " + ИмяВТ;
	
	Возврат ИсполняемыйЗапрос.Выполнить();
КонецФункции

ИсполняемыйЗапрос = Новый Запрос();

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
